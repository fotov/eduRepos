Функция ПодсистемыИОтветственныеПоОбъекту(Знач ИмяОбъекта) Экспорт

	ЧастиИмени = СтрРазделить(ИмяОбъекта, ".");
	Если ЧастиИмени.Количество() =1 Тогда
		ИмяОбъекта = ЧастиИмени[0];
	ИначеЕсли ЧастиИмени.Количество() > 1 Тогда
		ИмяОбъекта = ЧастиИмени[0] + "." + ЧастиИмени[1];
	КонецЕсли; 
	
	Результат = Новый Структура;
	Результат.Вставить("Подсистемы",	"Не определена");
	Результат.Вставить("Ответственные",	"Не определен");
	
	ИмяСервера = "izb-app20";
	АдресРесурса = "sppr/hs/structure/binding/Izbenka/" + ИмяОбъекта;
	
	Соединение = Новый HTTPСоединение(ИмяСервера,,"ФотовА","");
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	
	Попытка
	
		РезультатЗапроса = Соединение.Получить(Запрос);
		РезультатСтрокой = РезультатЗапроса.ПолучитьТелоКакСтроку();
		
		Если РезультатЗапроса.КодСостояния = 200 Тогда
		
			СтруктураОтвета = СтроковыеФункцииУККлиентСервер.ПрочитатьЗначениеJSON(РезультатСтрокой);
			Ответственные = Новый Массив;
			Для каждого ОписаниеОтветственного Из СтруктураОтвета.Ответственные Цикл
				Ответственные.Добавить(ОписаниеОтветственного.Ответственный);
			КонецЦикла;
			Результат.Подсистемы = СтрСоединить(СтруктураОтвета.Подсистемы, ", ");
			Результат.Ответственные = СтрСоединить(Ответственные, ", ");
		
		Иначе
		
			ВызватьИсключение СтрШаблон("Код возврата: %1
				| %2", РезультатЗапроса.КодСостояния, РезультатСтрокой);
		
		КонецЕсли;
	
	Исключение
		
		ОписаниеОшибки = ОписаниеОшибки();
		ЗаписьЖурналаРегистрации("СППР.ПолучениеОтветственных",
			УровеньЖурналаРегистрации.Ошибка,
			,					// Объект
			,					// Данные
			ОписаниеОшибки);	// Комментарий
			
		Результат.Подсистемы =		ОписаниеОшибки;
		Результат.Ответственные =	ОписаниеОшибки;
	
	КонецПопытки;
	
	Возврат Результат;

КонецФункции