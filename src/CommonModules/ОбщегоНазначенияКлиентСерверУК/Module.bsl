//////////////////////////////////////////////////////////////////////////////////////////////////////////
// Методы общей функциональности конфигурации
//  
//////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Получает HTML из табличного документа
//
// Параметры:
//  ТабличныйДокумент	 - ТабличныйДокумент 
// 
// Возвращаемое значение:
//  Строка - Строка в формате HTML текста
//
Функция ПолучитьHTMLИзТабличногоДокумента(ТабличныйДокумент) Экспорт
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("html");
	ТабличныйДокумент.Записать(ИмяВременногоФайла, ТипФайлаТабличногоДокумента.HTML);
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ИмяВременногоФайла);
	ТекстHTML = ТекстовыйДокумент.ПолучитьТекст();
	Попытка
		УдалитьФайлы(ИмяВременногоФайла);
	Исключение
		ИмяСобытия = "Удаление временного файла";
		Комментарий = "Не удалось удалить временный файл " + ИмяВременногоФайла;
		#Если Клиент Тогда
		ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(
			ИмяСобытия,
			"Предупреждение",
			Комментарий,
			,
			Истина);
		#Иначе
		ЗаписьЖурналаРегистрации(
			ИмяСобытия,
			УровеньЖурналаРегистрации.Предупреждение,
			,
			,
			Комментарий);
		#КонецЕсли
	КонецПопытки;
	Возврат ТекстHTML;
КонецФункции

Процедура ОбъединитьТабличныеДокументы(Приемник, Источник, Знач МаксимальнаяВысота = 0) Экспорт
	Если МаксимальнаяВысота = 0 Тогда
		МаксимальнаяВысота = Источник.ВысотаТаблицы;
	КонецЕсли;
	НачалоНовогоФорматаСтрок = Приемник.ВысотаТаблицы + 1;
	ОбластьПрямоугольная = Источник.Область(1, , МаксимальнаяВысота, );
	Приемник.ВставитьОбласть(ОбластьПрямоугольная, Приемник.Область(НачалоНовогоФорматаСтрок, 1));
	Для Ряды = 0 По МаксимальнаяВысота - 1 Цикл
		ОбластьСтрока = Приемник.Область(НачалоНовогоФорматаСтрок + Ряды, , НачалоНовогоФорматаСтрок + Ряды, );
		Если НЕ ЗначениеЗаполнено(ОбластьСтрока.Текст) Тогда
			Продолжить;
		КонецЕсли;
		ОбластьСтрока.СоздатьФорматСтрок();
		Для Колонки = 1 По Источник.ШиринаТаблицы Цикл
			Если Источник.Область(Ряды + 1, Колонки).ШиринаКолонки = 0 Тогда
				Продолжить;
			КонецЕсли;
			Приемник.Область(НачалоНовогоФорматаСтрок + Ряды, Колонки).ШиринаКолонки = Источник.Область(Ряды + 1, Колонки).ШиринаКолонки;
		КонецЦикла;
	КонецЦикла;
	Приемник.Область(НачалоНовогоФорматаСтрок + 1, , НачалоНовогоФорматаСтрок + МаксимальнаяВысота - 1).Сгруппировать();
	Приемник.ВывестиГоризонтальныйРазделительСтраниц();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
#КонецОбласти