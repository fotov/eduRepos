//////////////////////////////////////////////////////////////////////////////////////////////////////////
// Методы для реализации мониторинга системы
//  
//////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Процедура ЗаписатьНачалоОперации(КлючеваяОперация, Идентификатор = Неопределено, Комментарий = "") Экспорт
	
	ЗаписатьМониторингОперации(
		КлючеваяОперация,
		Идентификатор,
		Перечисления.ЭтапыОперацийМониторинга.Начало,
		Комментарий);
	
КонецПроцедуры

Процедура ЗаписатьЗавершениеОперации(КлючеваяОперация, Идентификатор = Неопределено, Комментарий = "", ВыполненСОшибкой = Ложь) Экспорт
	
	ЗаписатьМониторингОперации(
		КлючеваяОперация,
		Идентификатор,
		Перечисления.ЭтапыОперацийМониторинга.Завершение,
		Комментарий,
		ВыполненСОшибкой);
	
КонецПроцедуры

Процедура ЗаписатьМониторингОперации(КлючеваяОперация, Идентификатор = Неопределено, Этап = Неопределено, Комментарий = "", ВыполненСОшибкой = Ложь, Значение = 0) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.ЖурналМониторинга.СоздатьНаборЗаписей();

	Запись = НаборЗаписей.Добавить();	
	Запись.КлючеваяОперация = КлючеваяОперация;
	Запись.Идентификатор = Идентификатор;
	Запись.ДатаЗаписиУниверсальная = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Запись.НомерСеанса = НомерСеансаИнформационнойБазы();
	Запись.Этап = Этап;
	Запись.ДатаЗаписи = МестноеВремя(Дата(1,1,1) + Запись.ДатаЗаписиУниверсальная/1000);
	Запись.ДатаЗаписиНачалоДня = НачалоДня(Запись.ДатаЗаписи);
	Запись.Комментарий = Комментарий;
	Запись.ВыполненСОшибкой = ВыполненСОшибкой;
	Запись.Значение = Значение;
	
	НаборЗаписей.Записать(Ложь);
	
КонецПроцедуры

Функция ПользователиПоВидуМониторинга(ВидМониторинга) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПодпискиНаМониторинг.Пользователь КАК Пользователь
		|ИЗ
		|	РегистрСведений.ПодпискиНаМониторинг КАК ПодпискиНаМониторинг
		|ГДЕ
		|	ПодпискиНаМониторинг.ВидМониторинга = &ВидМониторинга";
	
	Запрос.УстановитьПараметр("ВидМониторинга", ВидМониторинга);
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Пользователь");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
#КонецОбласти