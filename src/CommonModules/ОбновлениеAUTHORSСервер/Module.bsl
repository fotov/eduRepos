#Область ПрограммныйИнтерфейс

Процедура ВыполнитьОбновлениеФайловAUTHORSПоПараметрам(Пользователи) Экспорт

	Если ТипЗнч(Пользователи) = Тип("Массив") Тогда
		МассивОбъектов = Пользователи;
	Иначе	
	    МассивОбъектов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Пользователи);
	КонецЕсли; 
	
	ЕстьОшибка = Ложь;
	
	ПодготовленныеПараметры = ПараметрыДляОбновленияAUTHORS(МассивОбъектов);
	
	МассивФайловыхОбъектов = НайтиФайлы(РаботаСХранилищами.КаталогGit1С(), "*");
	Для каждого Файл Из МассивФайловыхОбъектов Цикл
		Если НЕ Файл.ЭтоКаталог() Тогда
		    Продолжить;
		КонецЕсли; 
		
		Попытка
			ВыполнитьОбновлениеФайловAUTHORSВГитРепозитории(Файл.ПолноеИмя, ПодготовленныеПараметры);
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон("Ошибка обновления AUTHORS в гит-репозитарии %1 по причине: %2", 
																Файл.ПолноеИмя, 
																ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())), , , , ЕстьОшибка);
		КонецПопытки; 
	КонецЦикла; 
	
	Если НЕ ЕстьОшибка Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Обновление успешно выполнено"); 
	КонецЕсли; 
	
КонецПроцедуры
	
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыДляОбновленияAUTHORS(МассивОбъектов) 

	РезультатФункции = Новый Массив;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Пользователи.Ссылка КАК Ссылка,
		|	Пользователи.Код КАК Код,
		|	Пользователи.Наименование КАК Наименование,
		|	Пользователи.email КАК email
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(Выборка.email) Тогда
			Продолжить;
		КонецЕсли; 
		
		ПараметрыПользователя = Новый Структура;
		ПараметрыПользователя.Вставить("Логин", 						СокрЛП(Выборка.Код));
		ПараметрыПользователя.Вставить("АдресЭлектроннойПочты", 		Выборка.email);
		ПараметрыПользователя.Вставить("СтрокаОбъявленияПользователя", 	СтрШаблон("%1=%1 <%2>", СокрЛП(Выборка.Код), Выборка.email));
		
		РезультатФункции.Добавить(ПараметрыПользователя);
	КонецЦикла;

	Возврат РезультатФункции;

КонецФункции 

Процедура ВыполнитьОбновлениеФайловAUTHORSВГитРепозитории(ПолноеИмяКаталогаГитРепозитория, ПараметрыПользователей)
	
	НайденныеФайлы = НайтиФайлы(ПолноеИмяКаталогаГитРепозитория, ИмяФайлаAUTHORS(), Истина);
	
	Для каждого Файл Из НайденныеФайлы Цикл
		Если Файл.ЭтоКаталог() Тогда
			Продолжить;
		КонецЕсли; 
		
		ЕстьДанныеДляЗаписи = Ложь;
		
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.Прочитать(Файл.ПолноеИмя);
		ДанныеСтрокой = ТекстовыйДокумент.ПолучитьТекст();
		
		Для каждого ПараметрыПользователя Из ПараметрыПользователей Цикл
			Если СтрНайти(ДанныеСтрокой, СтрШаблон("%1=", ПараметрыПользователя.Логин)) Тогда
			    Продолжить;
			КонецЕсли; 
			
		    ТекстовыйДокумент.ДобавитьСтроку(ПараметрыПользователя.СтрокаОбъявленияПользователя);
			ЕстьДанныеДляЗаписи = Истина;
		КонецЦикла; 
		
		Если ЕстьДанныеДляЗаписи Тогда
			ТекстовыйДокумент.Записать(Файл.ПолноеИмя);
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

Функция ИмяФайлаAUTHORS()
	Возврат "AUTHORS";	
КонецФункции 

#КонецОбласти 

