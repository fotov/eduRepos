//////////////////////////////////////////////////////////////////////////////////////////////////////////
// Методы для реализации мониторинга системы
//  
//////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Процедура МониторингПроверкиАПК_ОбработчикЗадания() Экспорт

	КлючеваяОперацияПроверка = Справочники.КлючевыеОперацииМониторинга.АвтоматизированнаяПроверкаВерсииХранилища; 
	ЦелеваяДлительность = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КлючеваяОперацияПроверка, "ЦелевоеВремя");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЖурналМониторингаНачатыеОперации.КлючеваяОперация КАК КлючеваяОперация,
		|	ЖурналМониторингаНачатыеОперации.Идентификатор КАК Версия,
		|	ЖурналМониторингаНачатыеОперации.Идентификатор.Код КАК ВерсияКод,
		|	ЖурналМониторингаНачатыеОперации.ДатаЗаписиУниверсальная КАК ДатаЗаписиУниверсальная,
		|	ЖурналМониторингаНачатыеОперации.Этап КАК Этап,
		|	ВЫРАЗИТЬ(ЖурналМониторингаНачатыеОперации.Идентификатор КАК Справочник.ИсторияХранилища).Владелец КАК Хранилище
		|ПОМЕСТИТЬ АктивныеОперации
		|ИЗ
		|	РегистрСведений.ЖурналМониторинга КАК ЖурналМониторингаНачатыеОперации
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналМониторинга КАК ЖурналМониторингаЗавершенныеОперации
		|		ПО ЖурналМониторингаНачатыеОперации.КлючеваяОперация = ЖурналМониторингаЗавершенныеОперации.КлючеваяОперация
		|			И ЖурналМониторингаНачатыеОперации.Идентификатор = ЖурналМониторингаЗавершенныеОперации.Идентификатор
		|			И (ЖурналМониторингаНачатыеОперации.Этап = &ЭтапНачало)
		|			И (ЖурналМониторингаЗавершенныеОперации.Этап = &ЭтапЗавершение)
		|ГДЕ
		|	ИСТИНА
		|	И ЖурналМониторингаНачатыеОперации.Этап = &ЭтапНачало
		|	И ЖурналМониторингаЗавершенныеОперации.КлючеваяОперация ЕСТЬ NULL
		|	И ЖурналМониторингаНачатыеОперации.КлючеваяОперация = &КлючеваяОперация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АктивныеОперации.ВерсияКод КАК ВерсияКод,
		|	АктивныеОперации.Хранилище КАК Хранилище,
		|	АктивныеОперации.КлючеваяОперация КАК КлючеваяОперация,
		|	АктивныеОперации.Версия КАК Версия,
		|	АктивныеОперации.ДатаЗаписиУниверсальная КАК ДатаЗаписиУниверсальная,
		|	АктивныеОперации.Этап КАК Этап
		|ПОМЕСТИТЬ ТекущиеАктивныеОперации
		|ИЗ
		|	АктивныеОперации КАК АктивныеОперации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			МИНИМУМ(АктивныеОперации.ВерсияКод) КАК ВерсияКод,
		|			АктивныеОперации.Хранилище КАК Хранилище
		|		ИЗ
		|			АктивныеОперации КАК АктивныеОперации
		|		
		|		СГРУППИРОВАТЬ ПО
		|			АктивныеОперации.Хранилище) КАК СрезПервыхАктивныхОпераций
		|		ПО АктивныеОперации.ВерсияКод = СрезПервыхАктивныхОпераций.ВерсияКод
		|			И АктивныеОперации.Хранилище = СрезПервыхАктивныхОпераций.Хранилище
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЖурналМониторинга.КлючеваяОперация КАК КлючеваяОперация,
		|	ЖурналМониторинга.Идентификатор КАК Идентификатор,
		|	ЖурналМониторинга.ДатаЗаписиУниверсальная КАК ДатаЗаписиУниверсальная,
		|	ЖурналМониторинга.НомерСеанса КАК НомерСеанса,
		|	ЖурналМониторинга.ДатаЗаписиНачалоДня КАК ДатаЗаписиНачалоДня,
		|	ЖурналМониторинга.Этап КАК Этап,
		|	ЖурналМониторинга.Комментарий КАК Комментарий,
		|	ЖурналМониторинга.ДатаЗаписи КАК ДатаЗаписи,
		|	ЖурналМониторинга.Идентификатор.Владелец КАК ИдентификаторВладелец,
		|	ЖурналМониторинга.Идентификатор.Код КАК ИдентификаторКод
		|ПОМЕСТИТЬ ПредыдущиеЗавершенныеОперации
		|ИЗ
		|	РегистрСведений.ЖурналМониторинга КАК ЖурналМониторинга
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТекущиеАктивныеОперации КАК ТекущиеАктивныеОперации
		|		ПО (ЖурналМониторинга.КлючеваяОперация = &КлючеваяОперация)
		|			И ЖурналМониторинга.Идентификатор.Код < ТекущиеАктивныеОперации.ВерсияКод
		|			И ЖурналМониторинга.Идентификатор.Владелец = ТекущиеАктивныеОперации.Хранилище
		|ГДЕ
		|	ЖурналМониторинга.Этап = &ЭтапЗавершение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПредыдущиеЗавершенныеОперации.КлючеваяОперация КАК КлючеваяОперация,
		|	ПредыдущиеЗавершенныеОперации.Идентификатор КАК Идентификатор,
		|	ПредыдущиеЗавершенныеОперации.ДатаЗаписиУниверсальная КАК ДатаЗаписиУниверсальная,
		|	ПредыдущиеЗавершенныеОперации.НомерСеанса КАК НомерСеанса,
		|	ПредыдущиеЗавершенныеОперации.ДатаЗаписиНачалоДня КАК ДатаЗаписиНачалоДня,
		|	ПредыдущиеЗавершенныеОперации.Этап КАК Этап,
		|	ПредыдущиеЗавершенныеОперации.Комментарий КАК Комментарий,
		|	ПредыдущиеЗавершенныеОперации.ДатаЗаписи КАК ДатаЗаписи,
		|	ПредыдущиеЗавершенныеОперации.ИдентификаторВладелец КАК ИдентификаторВладелец,
		|	ПредыдущиеЗавершенныеОперации.ИдентификаторКод КАК ИдентификаторКод
		|ПОМЕСТИТЬ КрайниеПредыдущиеОперации
		|ИЗ
		|	ПредыдущиеЗавершенныеОперации КАК ПредыдущиеЗавершенныеОперации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ПредыдущиеЗавершенныеОперации.ИдентификаторВладелец КАК ИдентификаторВладелец,
		|			МАКСИМУМ(ПредыдущиеЗавершенныеОперации.ДатаЗаписиУниверсальная) КАК ДатаЗаписиУниверсальная
		|		ИЗ
		|			ПредыдущиеЗавершенныеОперации КАК ПредыдущиеЗавершенныеОперации
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ПредыдущиеЗавершенныеОперации.ИдентификаторВладелец) КАК ВремяПоследнихЗавершенныхОпераций
		|		ПО (ВремяПоследнихЗавершенныхОпераций.ИдентификаторВладелец = ПредыдущиеЗавершенныеОперации.ИдентификаторВладелец)
		|			И (ВремяПоследнихЗавершенныхОпераций.ДатаЗаписиУниверсальная = ПредыдущиеЗавершенныеОперации.ДатаЗаписиУниверсальная)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТекущиеАктивныеОперации.ВерсияКод КАК ВерсияКод,
		|	ПРЕДСТАВЛЕНИЕ(ТекущиеАктивныеОперации.Хранилище) КАК Хранилище,
		|	ТекущиеАктивныеОперации.Версия КАК Версия,
		|	ТекущиеАктивныеОперации.ДатаЗаписиУниверсальная КАК НачалоТекущийОперации,
		|	ЕСТЬNULL(КрайниеПредыдущиеОперации.ДатаЗаписиУниверсальная, ТекущиеАктивныеОперации.ДатаЗаписиУниверсальная) КАК ЗавершениеПредыдущейОперации,
		|	ЕСТЬNULL(КрайниеПредыдущиеОперации.Идентификатор, ЗНАЧЕНИЕ(Справочник.ИсторияХранилища.ПустаяСсылка)) КАК ПредыдущаяВерсия,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(КрайниеПредыдущиеОперации.ДатаЗаписиУниверсальная, ТекущиеАктивныеОперации.ДатаЗаписиУниверсальная) <= ТекущиеАктивныеОперации.ДатаЗаписиУниверсальная
		|			ТОГДА (&ТекущаяУниверсальнаяДатаВМиллисекундах - ТекущиеАктивныеОперации.ДатаЗаписиУниверсальная) / 1000
		|		ИНАЧЕ (&ТекущаяУниверсальнаяДатаВМиллисекундах - ЕСТЬNULL(КрайниеПредыдущиеОперации.ДатаЗаписиУниверсальная, ТекущиеАктивныеОперации.ДатаЗаписиУниверсальная)) / 1000
		|	КОНЕЦ КАК ДлительностьТекущейОперацииСекунд
		|ИЗ
		|	ТекущиеАктивныеОперации КАК ТекущиеАктивныеОперации
		|		ЛЕВОЕ СОЕДИНЕНИЕ КрайниеПредыдущиеОперации КАК КрайниеПредыдущиеОперации
		|		ПО ТекущиеАктивныеОперации.Хранилище = КрайниеПредыдущиеОперации.ИдентификаторВладелец
		|ГДЕ
		|	ВЫБОР
		|			КОГДА ЕСТЬNULL(КрайниеПредыдущиеОперации.ДатаЗаписиУниверсальная, ТекущиеАктивныеОперации.ДатаЗаписиУниверсальная) <= ТекущиеАктивныеОперации.ДатаЗаписиУниверсальная
		|				ТОГДА (&ТекущаяУниверсальнаяДатаВМиллисекундах - ТекущиеАктивныеОперации.ДатаЗаписиУниверсальная) / 1000
		|			ИНАЧЕ (&ТекущаяУниверсальнаяДатаВМиллисекундах - ЕСТЬNULL(КрайниеПредыдущиеОперации.ДатаЗаписиУниверсальная, ТекущиеАктивныеОперации.ДатаЗаписиУниверсальная)) / 1000
		|		КОНЕЦ / 60000 > &ПороговоеКоличествоМинутНаПроверкуАПК";
	
	Запрос.УстановитьПараметр("КлючеваяОперация", КлючеваяОперацияПроверка);
	Запрос.УстановитьПараметр("ЭтапНачало", Перечисления.ЭтапыОперацийМониторинга.Начало);
	Запрос.УстановитьПараметр("ЭтапЗавершение", Перечисления.ЭтапыОперацийМониторинга.Завершение);
	Запрос.УстановитьПараметр("ПороговоеКоличествоМинутНаПроверкуАПК", ЦелеваяДлительность);
	Запрос.УстановитьПараметр("ТекущаяУниверсальнаяДатаВМиллисекундах", ТекущаяУниверсальнаяДатаВМиллисекундах());
	
	Выборка = Запрос.Выполнить().Выбрать();
	ШаблонСообщения = "Длительность проверки АПК для коммита %1 (хранилище: %2, загружен %3) "
		+ "превышает пороговое нормативное значение %4 минут. Время проверки: %5 (предыдущая проверка завершилась в: %6).";
	ОповещаемыеПользователи = Мониторинг.ПользователиПоВидуМониторинга(Справочники.ВидыМониторинга.ПроверкаАПК);

	Пока Выборка.Следующий() Цикл
		ДатаНачалаОперации = МестноеВремя('00010101' + Выборка.НачалоТекущийОперации/1000);
		ДатаЗавершенияПредыдущейОперации = МестноеВремя('00010101' + Выборка.ЗавершениеПредыдущейОперации/1000);
		ТекстСообщения = СтрШаблон(
			ШаблонСообщения,
			Выборка.ВерсияКод, 
			Выборка.Хранилище, 
			Формат(ДатаНачалаОперации, "ДЛФ=DT"), 
			ЦелеваяДлительность,
			СтроковыеФункцииУККлиентСервер.ПредставлениеКоличестваВремени(Окр(Выборка.ДлительностьТекущейОперацииСекунд)),
			Формат(ДатаЗавершенияПредыдущейОперации, "ДЛФ=DT"));
		ВремяПовторнойОтправки = 300; // 5 минут
		ВнешниеДанные.ОтправитьСообщеие(ТекстСообщения, ВремяПовторнойОтправки, Истина, ОповещаемыеПользователи);
	КонецЦикла;
	
КонецПроцедуры

Процедура СинхронизацияХранилищаС_GIT_ОбработчикЗадания() Экспорт

	КлючеваяОперация = Справочники.КлючевыеОперацииМониторинга.СинхронизацияХранилищаС_GIT_ВыполнениеСкрипта;
	НормативноеВремяВыполнения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КлючеваяОперация, "ЦелевоеВремя");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЖурналМониторинга.Идентификатор КАК Идентификатор,
		|	ЖурналМониторинга.ДатаЗаписиУниверсальная КАК ДатаЗаписиУниверсальная,
		|	ЖурналМониторинга.ДатаЗаписи КАК ДатаЗаписи,
		|	ЖурналМониторинга.Идентификатор.Владелец КАК Хранилище
		|ПОМЕСТИТЬ КрайниеНачатыеОперации
		|ИЗ
		|	РегистрСведений.ЖурналМониторинга КАК ЖурналМониторинга
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ЖурналМониторинга.Идентификатор.Владелец КАК ИдентификаторВладелец,
		|			МАКСИМУМ(ЖурналМониторинга.ДатаЗаписиУниверсальная) КАК ДатаЗаписиУниверсальная
		|		ИЗ
		|			РегистрСведений.ЖурналМониторинга КАК ЖурналМониторинга
		|		ГДЕ
		|			ЖурналМониторинга.КлючеваяОперация = &КлючеваяОперация
		|			И ЖурналМониторинга.Этап = &ЭтапНачало
		|		СГРУППИРОВАТЬ ПО
		|			ЖурналМониторинга.Идентификатор.Владелец) КАК КрайниеНачатыеОперации
		|		ПО (КрайниеНачатыеОперации.ИдентификаторВладелец = ЖурналМониторинга.Идентификатор.Владелец)
		|		И (КрайниеНачатыеОперации.ДатаЗаписиУниверсальная = ЖурналМониторинга.ДатаЗаписиУниверсальная)
		|		И (ЖурналМониторинга.КлючеваяОперация = &КлючеваяОперация)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КрайниеНачатыеОперации.Идентификатор КАК Идентификатор,
		|	КрайниеНачатыеОперации.ДатаЗаписиУниверсальная КАК ДатаЗаписиУниверсальная,
		|	КрайниеНачатыеОперации.ДатаЗаписи КАК ДатаЗаписи,
		|	КрайниеНачатыеОперации.Хранилище КАК Хранилище,
		|	РАЗНОСТЬДАТ(КрайниеНачатыеОперации.ДатаЗаписи, &ТекущаяДата, СЕКУНДА) КАК ТекущаяДлительность
		|ИЗ
		|	КрайниеНачатыеОперации КАК КрайниеНачатыеОперации
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналМониторинга КАК ЖурналМониторинга
		|		ПО КрайниеНачатыеОперации.Хранилище = ЖурналМониторинга.Идентификатор.Владелец
		|		И КрайниеНачатыеОперации.ДатаЗаписиУниверсальная < ЖурналМониторинга.ДатаЗаписиУниверсальная
		|		И (ЖурналМониторинга.КлючеваяОперация = &КлючеваяОперация)
		|		И (ЖурналМониторинга.Этап = &ЭтапЗавершение)
		|ГДЕ
		|	ЖурналМониторинга.КлючеваяОперация ЕСТЬ NULL
		|	И РАЗНОСТЬДАТ(КрайниеНачатыеОперации.ДатаЗаписи, &ТекущаяДата, МИНУТА) > &НормативноеВремяВыполнения";
	
	Запрос.УстановитьПараметр("ЭтапЗавершение", Перечисления.ЭтапыОперацийМониторинга.Завершение);
	Запрос.УстановитьПараметр("НормативноеВремяВыполнения", НормативноеВремяВыполнения);
	Запрос.УстановитьПараметр("КлючеваяОперация", КлючеваяОперация);
	Запрос.УстановитьПараметр("ЭтапНачало", Перечисления.ЭтапыОперацийМониторинга.Начало);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	
	Выборка = Запрос.Выполнить().Выбрать();
	ШаблонСообщения = "Длительность операции Синхронизации с хранилищем %1 "
		+ "превышает пороговое нормативное значение %2 минут. Начало выполнения операции: %3. Время выполнения: %4.";
	ОповещаемыеПользователи = Мониторинг.ПользователиПоВидуМониторинга(Справочники.ВидыМониторинга.СинхронизацияХранилищаСGIT);

	Пока Выборка.Следующий() Цикл
		ТекстСообщения = СтрШаблон(
			ШаблонСообщения,
			Выборка.Хранилище, 
			НормативноеВремяВыполнения, 
			Формат(Выборка.ДатаЗаписи, "ДЛФ=DT"), 
			СтроковыеФункцииУККлиентСервер.ПредставлениеКоличестваВремени(Выборка.ТекущаяДлительность)
			);
		ВремяПовторнойОтправки = 300; // 5 минут
		ВнешниеДанные.ОтправитьСообщеие(ТекстСообщения, ВремяПовторнойОтправки, Истина, ОповещаемыеПользователи);
	КонецЦикла;
		
КонецПроцедуры

Процедура МониторингОбработкиЗадач_ОбработчикЗадания() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(ЗадачаИсполнителя.Ссылка) КАК Задача,
		|	ПРЕДСТАВЛЕНИЕ(ЗадачаИсполнителя.БизнесПроцесс) КАК БизнесПроцесс,
		|	ЗадачаИсполнителя.ДатаПоследнейПопытки КАК ДатаПоследнейПопытки,
		|	ЗадачаИсполнителя.КоличествоПопыток КАК КоличествоПопыток
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
		|ГДЕ
		|	НЕ ЗадачаИсполнителя.Выполнена
		|	И НЕ ЗадачаИсполнителя.БизнесПроцесс.ПометкаУдаления
		|	И ЗадачаИсполнителя.Заблокирована";
	
	Выборка = Запрос.Выполнить().Выбрать();
	ОповещаемыеПользователи = Мониторинг.ПользователиПоВидуМониторинга(Справочники.ВидыМониторинга.БлокировкаЗадачКонвеера);
	ШаблонСообщения = "Возникли ошибки при выполнении задачи. Задача %1 по бизнес процессу %2 заблокирована. "
	 + "Количество попыток выполнения: %3, крайняя попытка в %4.";
	
	Пока Выборка.Следующий() Цикл
		ТекстСообщения = СтрШаблон(
			ШаблонСообщения,
			Выборка.Задача, 
			Выборка.БизнесПроцесс, 
			Выборка.КоличествоПопыток, 
			Формат(Выборка.ДатаПоследнейПопытки, "ДЛФ=DT"), 
			);
		ВремяПовторнойОтправки = 300; // 5 минут
		ВнешниеДанные.ОтправитьСообщеие(ТекстСообщения, ВремяПовторнойОтправки, Истина, ОповещаемыеПользователи);
	КонецЦикла;
		
КонецПроцедуры

// Процедура обработки регламентного задания по очистке регистров мониторинга
Процедура ОчиститьДанныеМониторингаОбработчикЗадания() Экспорт
	
	ГраницаУдаления = НачалоДня(ТекущаяУниверсальнаяДата() - 86400*Константы.ПериодХраненияДанныхМониторинга.Получить());
	
	ЗапросЖурналМониторинга = Новый Запрос;
	ЗапросЖурналМониторинга.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЖурналМониторинга.ДатаЗаписиНачалоДня
	|ИЗ
	|	РегистрСведений.ЖурналМониторинга КАК ЖурналМониторинга
	|ГДЕ
	|	ЖурналМониторинга.ДатаЗаписиНачалоДня < &ГраницаУдаления";
	ЗапросЖурналМониторинга.УстановитьПараметр("ГраницаУдаления", ГраницаУдаления);
	Результат = ЗапросЖурналМониторинга.Выполнить();
	Выборка = Результат.Выбрать();
	
	НаборЗаписей = РегистрыСведений.ЖурналМониторинга.СоздатьНаборЗаписей();
	
	Пока Выборка.Следующий() Цикл
		НаборЗаписей.Отбор.ДатаЗаписиНачалоДня.Установить(Выборка.ДатаЗаписиНачалоДня);
		НаборЗаписей.Записать(Истина);
	КонецЦикла;
		
КонецПроцедуры

Процедура РасчетПоказателейМониторинга_ОбработчикЗадания() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Конфигурации.ХранилищеРазработки КАК Ссылка
		|ПОМЕСТИТЬ ХранилищаРазработки
		|ИЗ
		|	Справочник.Конфигурации КАК Конфигурации
		|ГДЕ
		|	НЕ Конфигурации.ХранилищеРазработки = ЗНАЧЕНИЕ(Справочник.Хранилища.ПустаяСсылка)
		|	И НЕ Конфигурации.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(ИсторияХранилища.Ссылка) КАК ЗначениеПоказателя,
		|	ХранилищаРазработки.Ссылка КАК Аналитика,
		|	ЗНАЧЕНИЕ(Справочник.ВидыМониторинга.СинхронизацияХранилищаСGIT) КАК ВидМониторинга,
		|	ЗНАЧЕНИЕ(Справочник.ПоказателиМониторинга.ДлинаОчереди) КАК ПоказательМониторинга
		|ПОМЕСТИТЬ ПоказателиДлиныОчереди
		|ИЗ
		|	ХранилищаРазработки КАК ХранилищаРазработки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИсторияХранилища КАК ИсторияХранилища
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ХешиGitВерсийХранилища КАК ХешиGitВерсийХранилища
		|			ПО ИсторияХранилища.Ссылка = ХешиGitВерсийХранилища.ВерсияХранилища
		|		ПО (НЕ ИсторияХранилища.ПометкаУдаления)
		|			И (ИсторияХранилища.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания)
		|ГДЕ
		|	ХешиGitВерсийХранилища.Хеш ЕСТЬ NULL
		|	И ХранилищаРазработки.Ссылка.ВыгружатьВGit
		|
		|СГРУППИРОВАТЬ ПО
		|	ХранилищаРазработки.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(ИсторияХранилища.Ссылка),
		|	ХранилищаРазработки.Ссылка,
		|	ЗНАЧЕНИЕ(Справочник.ВидыМониторинга.ПроверкаАПК),
		|	ЗНАЧЕНИЕ(Справочник.ПоказателиМониторинга.ДлинаОчереди)
		|ИЗ
		|	ХранилищаРазработки КАК ХранилищаРазработки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИсторияХранилища КАК ИсторияХранилища
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РезультатыСинтаксическойПроверки КАК РезультатыСинтаксическойПроверки
		|			ПО (РезультатыСинтаксическойПроверки.ОбъектПроверки = ИсторияХранилища.Ссылка)
		|		ПО ХранилищаРазработки.Ссылка = ИсторияХранилища.Владелец
		|			И (НЕ ИсторияХранилища.ПометкаУдаления)
		|			И (ИсторияХранилища.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания)
		|ГДЕ
		|	(РезультатыСинтаксическойПроверки.ОбъектПроверки ЕСТЬ NULL
		|			ИЛИ НЕ РезультатыСинтаксическойПроверки.ПровереноАПК)
		|
		|СГРУППИРОВАТЬ ПО
		|	ХранилищаРазработки.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(ИсторияХранилища.Ссылка),
		|	ХранилищаРазработки.Ссылка,
		|	ЗНАЧЕНИЕ(Справочник.ВидыМониторинга.ОпределениеЗадачиВерсии),
		|	ЗНАЧЕНИЕ(Справочник.ПоказателиМониторинга.ДлинаОчереди)
		|ИЗ
		|	ХранилищаРазработки КАК ХранилищаРазработки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИсторияХранилища КАК ИсторияХранилища
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиМетеорВерсийХранилища КАК ЗадачиМетеорВерсийХранилища
		|			ПО ИсторияХранилища.Ссылка = ЗадачиМетеорВерсийХранилища.ВерсияХранилища
		|		ПО ХранилищаРазработки.Ссылка = ИсторияХранилища.Владелец
		|			И (НЕ ИсторияХранилища.ПометкаУдаления)
		|			И (ИсторияХранилища.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания)
		|ГДЕ
		|	ЗадачиМетеорВерсийХранилища.ЗадачаМетеор ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ХранилищаРазработки.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РАЗНОСТЬДАТ(ПоследниеДаты.Дата, &ТекущаяДата, СЕКУНДА) КАК ПрошлоВремениСПоследнегоОбновления,
		|	ПоследниеДаты.Хранилище КАК Хранилище,
		|	ЗНАЧЕНИЕ(Справочник.ВидыМониторинга.ЗависаниеПолученияИсторииХранилища) КАК ВидМониторинга,
		|	ЗНАЧЕНИЕ(Справочник.ПоказателиМониторинга.ВремяОбработки) КАК ПоказательМониторинга
		|ПОМЕСТИТЬ ВремяОжиданияОбновленияХранилища
		|ИЗ
		|	(ВЫБРАТЬ
		|		МАКСИМУМ(ИсторияОбновленияХранилища.Дата) КАК Дата,
		|		ИсторияОбновленияХранилища.Хранилище КАК Хранилище
		|	ИЗ
		|		РегистрСведений.ИсторияОбновленияХранилища КАК ИсторияОбновленияХранилища
		|	ГДЕ
		|		ИсторияОбновленияХранилища.Хранилище В
		|				(ВЫБРАТЬ
		|					ХранилищаРазработки.Ссылка
		|				ИЗ
		|					ХранилищаРазработки)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ИсторияОбновленияХранилища.Хранилище) КАК ПоследниеДаты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВремяОжиданияОбновленияХранилища.ПрошлоВремениСПоследнегоОбновления КАК ЗначениеПоказателя,
		|	ВремяОжиданияОбновленияХранилища.Хранилище КАК Аналитика,
		|	ЗНАЧЕНИЕ(Справочник.ВидыМониторинга.ЗависаниеПолученияИсторииХранилища) КАК ВидМониторинга,
		|	ЗНАЧЕНИЕ(Справочник.ПоказателиМониторинга.ВремяОбработки) КАК ПоказательМониторинга
		|ПОМЕСТИТЬ ПоказателиВремениОбработки
		|ИЗ
		|	ВремяОжиданияОбновленияХранилища КАК ВремяОжиданияОбновленияХранилища
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			МАКСИМУМ(ВремяОжиданияОбновленияХранилища.ПрошлоВремениСПоследнегоОбновления) КАК ПрошлоВремениСПоследнегоОбновления
		|		ИЗ
		|			ВремяОжиданияОбновленияХранилища КАК ВремяОжиданияОбновленияХранилища) КАК ПоследниеДаты
		|		ПО ВремяОжиданияОбновленияХранилища.ПрошлоВремениСПоследнегоОбновления = ПоследниеДаты.ПрошлоВремениСПоследнегоОбновления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИсторияХранилища.Владелец КАК Владелец,
		|	ИсторияХранилища.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ПоследниеКоммитыДляРасчетаСреднего
		|ИЗ
		|	(ВЫБРАТЬ
		|		МАКСИМУМ(ИсторияХранилища.Код) - &КоличествоДляРасчетаСреднего КАК Код,
		|		ИсторияХранилища.Владелец КАК Владелец
		|	ИЗ
		|		Справочник.ИсторияХранилища КАК ИсторияХранилища
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РезультатыСинтаксическойПроверки КАК РезультатыСинтаксическойПроверки
		|			ПО (РезультатыСинтаксическойПроверки.ОбъектПроверки = ИсторияХранилища.Ссылка)
		|	ГДЕ
		|		РезультатыСинтаксическойПроверки.ПровереноАПК
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ИсторияХранилища.Владелец) КАК СрезПервыеКоммитыДляРасчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИсторияХранилища КАК ИсторияХранилища
		|		ПО СрезПервыеКоммитыДляРасчета.Владелец = ИсторияХранилища.Владелец
		|			И СрезПервыеКоммитыДляРасчета.Код < ИсторияХранилища.Код
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИсторияХранилища.Ссылка КАК ВерсияХранилища
		|ПОМЕСТИТЬ ВерсииЗагруженыДанные
		|ИЗ
		|	РегистрСведений.РезультатыСинтаксическойПроверки КАК РезультатыСинтаксическойПроверки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИсторияХранилища КАК ИсторияХранилища
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиМетеорВерсийХранилища КАК ЗадачиМетеорВерсийХранилища
		|			ПО (ЗадачиМетеорВерсийХранилища.ВерсияХранилища = ИсторияХранилища.Ссылка)
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ХешиGitВерсийХранилища КАК ХешиGitВерсийХранилища
		|			ПО (ХешиGitВерсийХранилища.ВерсияХранилища = ИсторияХранилища.Ссылка)
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПоказателиВерсийХранилища КАК ПоказателиВерсийХранилища
		|			ПО (ПоказателиВерсийХранилища.ВерсияХранилища = ИсторияХранилища.Ссылка)
		|		ПО РезультатыСинтаксическойПроверки.ОбъектПроверки = ИсторияХранилища.Ссылка
		|ГДЕ
		|	НЕ ЗадачиМетеорВерсийХранилища.ВерсияХранилища ЕСТЬ NULL
		|	И (ЕСТЬNULL(ПоказателиВерсийХранилища.ПроверкаКодаВручную, ЛОЖЬ)
		|			ИЛИ ЕСТЬNULL(РезультатыСинтаксическойПроверки.ПровереноАПК, ЛОЖЬ))
		|	И НЕ ХешиGitВерсийХранилища.ВерсияХранилища ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИсторияХранилища.Владелец КАК Владелец,
		|	ИсторияХранилища.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ПоследниеКоммитыДляРасчетаСреднегоЗагрузкаОбработка
		|ИЗ
		|	(ВЫБРАТЬ
		|		МАКСИМУМ(ИсторияХранилища.Код) - &КоличествоДляРасчетаСреднего КАК Код,
		|		ИсторияХранилища.Владелец КАК Владелец
		|	ИЗ
		|		Справочник.ИсторияХранилища КАК ИсторияХранилища
		|	ГДЕ
		|		ИсторияХранилища.Ссылка В
		|				(ВЫБРАТЬ
		|					ВерсииЗагруженыДанные.ВерсияХранилища КАК ВерсияХранилища
		|				ИЗ
		|					ВерсииЗагруженыДанные КАК ВерсииЗагруженыДанные)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ИсторияХранилища.Владелец) КАК СрезПервыеКоммитыДляРасчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИсторияХранилища КАК ИсторияХранилища
		|		ПО СрезПервыеКоммитыДляРасчета.Владелец = ИсторияХранилища.Владелец
		|			И СрезПервыеКоммитыДляРасчета.Код < ИсторияХранилища.Код
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 10000
		|	ЖурналМониторинга.КлючеваяОперация КАК КлючеваяОперация,
		|	ЖурналМониторинга.Идентификатор КАК ВерсияХранилища,
		|	ЖурналМониторинга.Значение КАК Значение,
		|	ВЫРАЗИТЬ(ЖурналМониторинга.Идентификатор КАК Справочник.ИсторияХранилища).Владелец КАК Хранилище,
		|	ЖурналМониторинга.ДатаЗаписиУниверсальная КАК ДатаЗаписиУниверсальная
		|ПОМЕСТИТЬ ВремяВыполненияСинхронизацииСГит_Подготовка
		|ИЗ
		|	РегистрСведений.ЖурналМониторинга КАК ЖурналМониторинга
		|ГДЕ
		|	ЖурналМониторинга.КлючеваяОперация = ЗНАЧЕНИЕ(Справочник.КлючевыеОперацииМониторинга.СинхронизацияХранилищаС_GIT_ВыполнениеСкрипта)
		|	И ЖурналМониторинга.Значение >= &МинимальноеУчитываемоеВремяВыгрузкиВГит
		|	И НЕ ЖурналМониторинга.ВыполненСОшибкой
		|	И ВЫРАЗИТЬ(ЖурналМониторинга.Идентификатор КАК Справочник.ИсторияХранилища).Владелец В
		|			(ВЫБРАТЬ
		|				ХранилищаРазработки.Ссылка
		|			ИЗ
		|				ХранилищаРазработки)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СрезПоследнихСинхронизацииСГит.КлючеваяОперация КАК КлючеваяОперация,
		|	СРЕДНЕЕ(СрезПоследнихСинхронизацииСГит.Значение) КАК Значение,
		|	СрезПоследнихСинхронизацииСГит.Хранилище КАК Хранилище
		|ПОМЕСТИТЬ СреднееВремяСинхронизацииХранилищ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ИскомыеЗаписи.КлючеваяОперация КАК КлючеваяОперация,
		|		ИскомыеЗаписи.Значение КАК Значение,
		|		ИскомыеЗаписи.Хранилище КАК Хранилище
		|	ИЗ
		|		ВремяВыполненияСинхронизацииСГит_Подготовка КАК ИскомыеЗаписи
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВремяВыполненияСинхронизацииСГит_Подготовка КАК ЗаписиПозжеИскомой
		|			ПО ИскомыеЗаписи.КлючеваяОперация = ЗаписиПозжеИскомой.КлючеваяОперация
		|				И ИскомыеЗаписи.Хранилище = ЗаписиПозжеИскомой.Хранилище
		|				И ИскомыеЗаписи.ДатаЗаписиУниверсальная <= ЗаписиПозжеИскомой.ДатаЗаписиУниверсальная
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ИскомыеЗаписи.КлючеваяОперация,
		|		ИскомыеЗаписи.Значение,
		|		ИскомыеЗаписи.Хранилище
		|	
		|	ИМЕЮЩИЕ
		|		КОЛИЧЕСТВО(*) <= &КоличествоДляРасчетаСреднего) КАК СрезПоследнихСинхронизацииСГит
		|
		|СГРУППИРОВАТЬ ПО
		|	СрезПоследнихСинхронизацииСГит.Хранилище,
		|	СрезПоследнихСинхронизацииСГит.КлючеваяОперация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СРЕДНЕЕ(ЖурналМониторинга.Значение) КАК СреднееВремяПроверкиСекунд,
		|	ПоследниеКоммитыДляРасчетаСреднего.Владелец КАК Хранилище,
		|	ЗНАЧЕНИЕ(Справочник.ВидыМониторинга.ПроверкаАПК) КАК ВидМониторинга,
		|	ЗНАЧЕНИЕ(Справочник.ПоказателиМониторинга.СреднееВремяОбработки) КАК Показатель
		|ПОМЕСТИТЬ ПоказателиСреднегоВремени
		|ИЗ
		|	РегистрСведений.ЖурналМониторинга КАК ЖурналМониторинга
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоследниеКоммитыДляРасчетаСреднего КАК ПоследниеКоммитыДляРасчетаСреднего
		|		ПО ЖурналМониторинга.Идентификатор = ПоследниеКоммитыДляРасчетаСреднего.Ссылка
		|			И (ЖурналМониторинга.КлючеваяОперация = ЗНАЧЕНИЕ(Справочник.КлючевыеОперацииМониторинга.ДлительностьПроверкиКоммитаАПК))
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоследниеКоммитыДляРасчетаСреднего.Владелец
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СРЕДНЕЕ(ЖурналМониторинга.Значение),
		|	ПоследниеКоммитыДляРасчетаСреднего.Владелец,
		|	ЗНАЧЕНИЕ(Справочник.ВидыМониторинга.ЗагрузкаИОбработкаВерсииИсторииХранилища),
		|	ЗНАЧЕНИЕ(Справочник.ПоказателиМониторинга.СреднееВремяОбработки)
		|ИЗ
		|	РегистрСведений.ЖурналМониторинга КАК ЖурналМониторинга
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоследниеКоммитыДляРасчетаСреднегоЗагрузкаОбработка КАК ПоследниеКоммитыДляРасчетаСреднего
		|		ПО ЖурналМониторинга.Идентификатор = ПоследниеКоммитыДляРасчетаСреднего.Ссылка
		|			И (ЖурналМониторинга.КлючеваяОперация = ЗНАЧЕНИЕ(Справочник.КлючевыеОперацииМониторинга.ЗагрузкаИОбработкаВерсииИсторииХранилища))
		|			И (ЖурналМониторинга.Этап = ЗНАЧЕНИЕ(Перечисление.ЭтапыОперацийМониторинга.Завершение))
		|			И (НЕ ЖурналМониторинга.Значение = 0)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоследниеКоммитыДляРасчетаСреднего.Владелец
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоказателиДлиныОчереди.ЗначениеПоказателя КАК Значение,
		|	ВЫБОР
		|		КОГДА ПоказателиДлиныОчереди.ЗначениеПоказателя = 0
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.Хранилища.ПустаяСсылка)
		|		ИНАЧЕ МАКСИМУМ(ПоказателиДлиныОчереди.Аналитика)
		|	КОНЕЦ КАК Хранилище,
		|	ПоказателиДлиныОчереди.ВидМониторинга КАК ВидМониторинга,
		|	ПоказателиДлиныОчереди.ПоказательМониторинга КАК Показатель
		|ИЗ
		|	(ВЫБРАТЬ
		|		МАКСИМУМ(ПоказателиДлиныОчереди.ЗначениеПоказателя) КАК ЗначениеПоказателя,
		|		ПоказателиДлиныОчереди.ВидМониторинга КАК ВидМониторинга,
		|		ПоказателиДлиныОчереди.ПоказательМониторинга КАК ПоказательМониторинга
		|	ИЗ
		|		ПоказателиДлиныОчереди КАК ПоказателиДлиныОчереди
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПоказателиДлиныОчереди.ВидМониторинга,
		|		ПоказателиДлиныОчереди.ПоказательМониторинга) КАК МаксимальнаяДлинаОчереди
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоказателиДлиныОчереди КАК ПоказателиДлиныОчереди
		|		ПО МаксимальнаяДлинаОчереди.ВидМониторинга = ПоказателиДлиныОчереди.ВидМониторинга
		|			И МаксимальнаяДлинаОчереди.ПоказательМониторинга = ПоказателиДлиныОчереди.ПоказательМониторинга
		|			И МаксимальнаяДлинаОчереди.ЗначениеПоказателя = ПоказателиДлиныОчереди.ЗначениеПоказателя
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоказателиДлиныОчереди.ЗначениеПоказателя,
		|	ПоказателиДлиныОчереди.ВидМониторинга,
		|	ПоказателиДлиныОчереди.ПоказательМониторинга
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПоказателиВремениОбработки.ЗначениеПоказателя,
		|	ПоказателиВремениОбработки.Аналитика,
		|	ПоказателиВремениОбработки.ВидМониторинга,
		|	ПоказателиВремениОбработки.ПоказательМониторинга
		|ИЗ
		|	ПоказателиВремениОбработки КАК ПоказателиВремениОбработки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПоказателиСреднегоВремени.СреднееВремяПроверкиСекунд,
		|	ПоказателиСреднегоВремени.Хранилище,
		|	ПоказателиСреднегоВремени.ВидМониторинга,
		|	ПоказателиСреднегоВремени.Показатель
		|ИЗ
		|	ПоказателиСреднегоВремени КАК ПоказателиСреднегоВремени
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			МАКСИМУМ(ПоказателиСреднегоВремени.СреднееВремяПроверкиСекунд) КАК СреднееВремяПроверкиСекунд,
		|			ПоказателиСреднегоВремени.ВидМониторинга КАК ВидМониторинга
		|		ИЗ
		|			ПоказателиСреднегоВремени КАК ПоказателиСреднегоВремени
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ПоказателиСреднегоВремени.ВидМониторинга) КАК МаксимальноеСреднее
		|		ПО ПоказателиСреднегоВремени.СреднееВремяПроверкиСекунд = МаксимальноеСреднее.СреднееВремяПроверкиСекунд
		|			И ПоказателиСреднегоВремени.ВидМониторинга = МаксимальноеСреднее.ВидМониторинга
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	МаксимальноеВремяСинхронизации.Значение,
		|	МаксимальноеВремяСинхронизации.Хранилище,
		|	МаксимальноеВремяСинхронизации.ВидМониторинга,
		|	МаксимальноеВремяСинхронизации.ПоказательМониторинга
		|ИЗ
		|	(ВЫБРАТЬ ПЕРВЫЕ 1
		|		СреднееВремяСинхронизацииХранилищ.Значение КАК Значение,
		|		СреднееВремяСинхронизацииХранилищ.Хранилище КАК Хранилище,
		|		ЗНАЧЕНИЕ(Справочник.ВидыМониторинга.СинхронизацияХранилищаСGIT) КАК ВидМониторинга,
		|		ЗНАЧЕНИЕ(Справочник.ПоказателиМониторинга.СреднееВремяОбработки) КАК ПоказательМониторинга
		|	ИЗ
		|		СреднееВремяСинхронизацииХранилищ КАК СреднееВремяСинхронизацииХранилищ
		|	ГДЕ
		|		СреднееВремяСинхронизацииХранилищ.Значение В
		|				(ВЫБРАТЬ
		|					МАКСИМУМ(МаксимальноеСреднее.Значение)
		|				ИЗ
		|					СреднееВремяСинхронизацииХранилищ КАК МаксимальноеСреднее)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		0,
		|		ЗНАЧЕНИЕ(Справочник.Хранилища.ПустаяСсылка),
		|		ЗНАЧЕНИЕ(Справочник.ВидыМониторинга.СинхронизацияХранилищаСGIT),
		|		ЗНАЧЕНИЕ(Справочник.ПоказателиМониторинга.СреднееВремяОбработки)) КАК МаксимальноеВремяСинхронизации
		|
		|УПОРЯДОЧИТЬ ПО
		|	Значение УБЫВ";
	
	ДатаСбораСтатистики = ТекущаяДата();
	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(ДатаСбораСтатистики));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ДатаСбораСтатистики));
	Запрос.УстановитьПараметр("ТекущаяДата", ДатаСбораСтатистики);
	Запрос.УстановитьПараметр("КоличествоДляРасчетаСреднего", 10);
	Запрос.УстановитьПараметр("МинимальноеУчитываемоеВремяВыгрузкиВГит", 40);

	Выборка = Запрос.Выполнить().Выбрать();

	Набор = РегистрыСведений.СтатистикаМониторинга.СоздатьНаборЗаписей();
	Пока Выборка.Следующий() Цикл
		Запись = Набор.Добавить();
		Запись.Период = ДатаСбораСтатистики;
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
	КонецЦикла;
	
	Набор.Записать(Ложь);
	
КонецПроцедуры

#КонецОбласти
