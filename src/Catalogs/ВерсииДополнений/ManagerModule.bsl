#Если Сервер Тогда
	
#Область СлужебныйПрограммныйИнтерфейс

// Имя соответствует шаблону.
// 
// Параметры:
//  Имя - Строка
// 
// Возвращаемое значение:
//  Структура - Имя соответствует шаблону:
// * ИмяСоответствуетШаблону - Булево 
// * Комментарий - Строка 
Функция ИмяСоответствуетШаблону(Имя) Экспорт
	
	// Решение "на скорую руку" без учёта возможности изменения шаблона.
	//Шаблон = "{Исправление|Доработка}_{КодКонфигурации}_{КодАвтора}_{Команда}_{[{ДоYYYYMMDD}]/[Постоянное]}[_{НомерЗадачи}][_v[НомерВерсии]][_{Описание}]";
	Результат = Новый Структура("ИмяСоответствуетШаблону, Комментарий", Ложь, "");

	Если СтрДлина(Имя) > 80 Тогда
		Результат.Комментарий = "Наименование больше 80 символов";
		Возврат Результат;
	КонецЕсли;
	
	Части = СтрРазделить(Имя, "_");
	Если Части.Количество() < 4 Тогда
		Результат.Комментарий = "Мало частей в наименовании";
		Возврат Результат;
	КонецЕсли;
	
	ИсправлениеДоработка = нрег(Части[0]);
	Если Не (ИсправлениеДоработка = "исправление" ИЛИ ИсправлениеДоработка = "доработка") Тогда
		Результат.Комментарий = "Не определено Исправление или Доработка";
		Возврат Результат;
	КонецЕсли;
	
	КодКонфигурации = Части[1];
	Если Справочники.Конфигурации.НайтиПоРеквизиту("Идентификатор", КодКонфигурации).Пустая() Тогда
		Результат.Комментарий = СтрШаблон("Не найдена конфигурация по коду ""%1""", КодКонфигурации);
		Возврат Результат;
	КонецЕсли;
	
	КодАвтора = Части[2];
	Если Справочники.Пользователи.НайтиПоКоду(КодАвтора).Пустая() Тогда
		Результат.Комментарий = СтрШаблон("Не найден автор по коду ""%1""", КодАвтора);
		Возврат Результат;
	КонецЕсли;

	Результат.ИмяСоответствуетШаблону = Истина;	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
