
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Элементы.ОткрытьСтандарт.Заголовок = "https://bookstack.int.vkusvill.ru/books/pravila-razrabotki-na-proekte-vv/page/vv-std-0010-trebovaniya-k-rassireniyam";
	
	Шаблон = "{Исправление|Доработка}_{КодКонфигурации}_{КодАвтора}_{Команда}_{[{ДоYYYYMMDD}]/[Постоянное]}[_{НомерЗадачи}][_v[НомерВерсии]][_{Описание}]";
	
	СтрокаШаблона = СоставШаблона.Добавить();
	СтрокаШаблона.Описание = "Тип расширения";
	СтрокаШаблона.Ключ = "ТипРасширения";
	СтрокаШаблона.Обязательный = Истина;
	СтрокаШаблона.Значение = Перечисления.ТипыРасширений.Исправление;
	
	СтрокаШаблона = СоставШаблона.Добавить();
	СтрокаШаблона.Описание = "Код конфигурации";
	СтрокаШаблона.Ключ = "КодКонфигурации";
	СтрокаШаблона.Обязательный = Истина;
	СтрокаШаблона.Значение = Справочники.Конфигурации.ПустаяСсылка();
	
	СтрокаШаблона = СоставШаблона.Добавить();
	СтрокаШаблона.Описание = "Код автора";
	СтрокаШаблона.Ключ = "КодАвтора";
	СтрокаШаблона.Обязательный = Истина;
	СтрокаШаблона.Значение = Справочники.Пользователи.ПустаяСсылка();
	
	СтрокаШаблона = СоставШаблона.Добавить();
	СтрокаШаблона.Описание = "Команда";
	СтрокаШаблона.Ключ = "Команда";
	СтрокаШаблона.Обязательный = Истина;
	СтрокаШаблона.Значение = Справочники.Команды.ПустаяСсылка();
	
	СтрокаШаблона = СоставШаблона.Добавить();
	СтрокаШаблона.Описание = "Дата актуальности";
	СтрокаШаблона.Ключ = "ДатаАктуальности";
	СтрокаШаблона.Обязательный = Истина;
	СтрокаШаблона.Значение = '00010101';
	
	СтрокаШаблона = СоставШаблона.Добавить();
	СтрокаШаблона.Описание = "Номер задачи";
	СтрокаШаблона.Ключ = "НомерЗадачи";
	СтрокаШаблона.Обязательный = Истина;
	СтрокаШаблона.Значение = Справочники.ЗадачиМетеор.ПустаяСсылка();
	
	СтрокаШаблона = СоставШаблона.Добавить();
	СтрокаШаблона.Описание = "Версия";
	СтрокаШаблона.Ключ = "НомерВерсии";
	СтрокаШаблона.Обязательный = Ложь;
	СтрокаШаблона.Значение = "";
	
	СтрокаШаблона = СоставШаблона.Добавить();
	СтрокаШаблона.Описание = "Описание";
	СтрокаШаблона.Ключ = "Описание";
	СтрокаШаблона.Обязательный = Ложь;
	СтрокаШаблона.Значение = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	СоставШаблонаПриИзменении(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_СоставШаблона

&НаКлиенте
Процедура СоставШаблонаПриИзменении(Элемент)
	
	Части = Новый Массив;
	
	Для Каждого СтрокаШаблона Из СоставШаблона Цикл
		
		Если ТипЗнч(СтрокаШаблона.Значение) = Тип("СправочникСсылка.Пользователи") Тогда
			Значение = ПолучитьКодПользователя(СтрокаШаблона.Значение);
		ИначеЕсли ТипЗнч(СтрокаШаблона.Значение) = Тип("СправочникСсылка.Конфигурации") Тогда
			Значение = ПолучитьИдКонфигурации(СтрокаШаблона.Значение);
		ИначеЕсли ТипЗнч(СтрокаШаблона.Значение) = Тип("СправочникСсылка.Команды") Тогда
			Значение = ПолучитьИдентификаторКоманды(СтрокаШаблона.Значение);
		ИначеЕсли ТипЗнч(СтрокаШаблона.Значение) = Тип("Дата") Тогда
			Если ЗначениеЗаполнено(СтрокаШаблона.Значение) Тогда
				Значение = "До" + Формат(СтрокаШаблона.Значение, "ЧГ=; ДФ=yyyyMMdd");
			ИначеЕсли СтрокаШаблона.Обязательный Тогда
				Значение = "Постоянное";
			Иначе
				Значение = "";
			КонецЕсли;
		ИначеЕсли СтрокаШаблона.Ключ = "НомерВерсии" И ЗначениеЗаполнено(СтрокаШаблона.Значение) Тогда
			Значение = "v" + СтрокаШаблона.Значение;
		Иначе
			Значение = Строка(СтрокаШаблона.Значение);
		КонецЕсли;
		СтрокаШаблона.ПредставлениеЗначения = Строка(Значение);
		
		Если ЗначениеЗаполнено(Значение) Тогда
			Части.Добавить(Значение);
		ИначеЕсли СтрокаШаблона.Обязательный Тогда
			Части.Добавить("Null");
		КонецЕсли;
		
	КонецЦикла;
	
	ИмяВерсииДополнения = СтрСоединить(Части, "_");
	
	УдалитьНедопустимыеСимволы(ИмяВерсииДополнения);
	
	ИмяВерсииДополнения = Лев(ИмяВерсииДополнения, 80);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьСтандарт(Команда)
	НачатьЗапускПриложения(Новый ОписаниеОповещения, Элементы.ОткрытьСтандарт.Заголовок);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьКодПользователя(Пользователь)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пользователь, "Код");
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьИдКонфигурации(Конфигурация)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Конфигурация, "Идентификатор");
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьИдентификаторКоманды(Команда)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Команда, "Наименование");
КонецФункции

&НаКлиенте
Процедура УдалитьНедопустимыеСимволы(Строка) Экспорт
	НедопустимыеСимволы = " ""'`/\[]{}:;|-=?*<>,.()+#№@!%^&~«»";
	Строка = СтрСоединить(СтрРазделить(Строка, НедопустимыеСимволы, Ложь));
КонецПроцедуры

#КонецОбласти
