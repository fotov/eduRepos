
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.МаксимальноеВремяЗахватаЧасов.ПодсказкаВвода = Объект.Владелец.МаксимальноеВремяЗахватаПоУмолчаниюЧасов;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьЛогическуюПодсистему(Команда)
	
	ОповещениеОВыборе = Новый ОписаниеОповещения("ПослеВыбораЛогическойПодсистемы", ЭтотОбъект);
	ПараметрыФормы = Новый Структура("УстановитьПользовательскийОтборПоТекущемуПользователю", Истина);
	ОткрытьФорму(
		"РегистрСведений.ОтветственныеЛогическихПодсистем.Форма.ФормаВыбора",
		ПараметрыФормы,
		ЭтаФорма,
		УникальныйИдентификатор,
		,
		,
		ОповещениеОВыборе,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеВыбораЛогическойПодсистемы(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПослеВыбораЛогическойПодсистемыНаСервере(Результат, ДополнительныеПараметры);

КонецПроцедуры

&НаСервере
Процедура ПослеВыбораЛогическойПодсистемыНаСервере(Результат, ДополнительныеПараметры) Экспорт
	УстановитьЛогическуюПодсистемуОбъектамНаСервере(Результат);
КонецПроцедуры

&НаСервере
Процедура УстановитьЛогическуюПодсистемуОбъектамНаСервере(ЛогическаяПодсистема)

	// Создаём связь
	Попытка
		МЗ = РегистрыСведений.СоставЛогическихПодсистем.СоздатьМенеджерЗаписи();
		МЗ.Период = ТекущаяДатаСеанса();
		МЗ.ОбъектМетаданных = Объект.Ссылка;
		МЗ.ЛогическаяПодсистема = ЛогическаяПодсистема;
		МЗ.Записать();
		ОбщегоНазначения.СообщитьПользователю("Связь объекта с подсистемой установлена.");
	Исключение
		ОбщегоНазначения.СообщитьПользователю(
			"Не удалось установить логическую подсистему для объекта метаданных: "
			+ Объект.Ссылка
			+ " по причине: "
			+ ОписаниеОшибки());
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

#КонецОбласти
