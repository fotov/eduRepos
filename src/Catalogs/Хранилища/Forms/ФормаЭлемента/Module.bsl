
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьОтборДинамическимСпискам();
	НастроитьФорму(ЭтаФорма);
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Элементы.ВерсияПлатформы.ПодсказкаВвода = СистемнаяИнформация.ВерсияПриложения;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	УстановитьОтборДинамическимСпискам();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипХранимыхДанныхПриИзменении(Элемент)
	НастроитьФорму(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ЗагружатьЗахваченныеОбъектыХранилищПриИзменении(Элемент)
	НастроитьФорму(ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьИсторию(Команда)
	
	ОбновитьИсториюНаСервере(Объект.Ссылка);
	
	Элементы.ИсторияХранилища.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьИсториюНаСервере(Хранилище)
	
	Если Не ЗначениеЗаполнено(Хранилище) Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСХранилищами.ОбновитьИсториюХранилища(Хранилище);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречитатьКоммит(Команда)
	
	ПеречитатьКоммитНаСервере(Элементы.ИсторияХранилища.ТекущаяСтрока);
	
	Элементы.ИсторияХранилища.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ПеречитатьКоммитНаСервере(Коммит)
	
	Если Не ЗначениеЗаполнено(Коммит) Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСХранилищами.ОбновитьВерсиюИсторииХранилища(Коммит);
	
КонецПроцедуры

&НаКлиенте
Процедура СинхронизироватьСGit(Команда)
	
	СинхронизироватьСGitНаСервере(Объект.Ссылка);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СинхронизироватьСGitНаСервере(Хранилище)
	
	РаботаСХранилищами.СинхронизироватьХранилищеСGit(Хранилище);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречитатьЗахваченныеОбъектыХранилищ(Команда)
	ПеречитатьЗахваченныеОбъектыХранилищНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПеречитатьЗахваченныеОбъектыХранилищНаСервере()
	РаботаСХранилищами.ЗаписатьЗахваченныеОбъекты(Объект.Ссылка);
	Элементы.ЗахваченныеОбъектыХранилищ.Обновить();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборДинамическимСпискам()
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ИсторияХранилища.Отбор, "Владелец", Объект.Ссылка,,, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ИсторияОбновления.Отбор, "Хранилище", Объект.Ссылка,,, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ЗахваченныеОбъектыХранилищ.Отбор, "Хранилище", Объект.Ссылка,,, Истина);
КонецПроцедуры

&НаКлиенте
Процедура КаталогРезультатовПайплайновОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	НачатьЗапускПриложения(Новый ОписаниеОповещения, Объект.КаталогРезультатовПайплайнов);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьФорму(Форма)
	Форма.Элементы.СтраницаЗахваченныеОбъектыХранилищ.Видимость = Форма.Объект.ЗагружатьЗахваченныеОбъектыХранилищ;
КонецПроцедуры


#КонецОбласти
