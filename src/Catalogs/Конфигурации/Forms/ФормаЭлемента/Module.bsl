#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Спр.Ссылка КАК ДействиеКонвейераОбработки,
		|	НЕ ПроектыШагиКонвейера.Ссылка ЕСТЬ NULL КАК Отметка,
		|	Спр.ПорядокСортировки КАК ПорядокСортировки
		|ИЗ
		|	Справочник.ДействияКонвейераОбработки КАК Спр
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Конфигурации.ШагиКонвейера КАК ПроектыШагиКонвейера
		|		ПО Спр.Ссылка = ПроектыШагиКонвейера.ДействиеКонвейераОбработки
		|			И (ПроектыШагиКонвейера.Ссылка = &Проект)
		|ГДЕ
		|	Спр.ПометкаУдаления = ЛОЖЬ
		|	И Спр.Родитель = ЗНАЧЕНИЕ(Справочник.ДействияКонвейераОбработки.ПустаяСсылка)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПорядокСортировки";
	Запрос.УстановитьПараметр("Проект", Объект.Ссылка);
	ШагиКонвейера.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюШагиКонвейераНаСервере()
	
	Объект.ШагиКонвейера.Очистить();
	Для Каждого Стр Из ШагиКонвейера.НайтиСтроки(Новый Структура("Отметка", Истина)) Цикл
		Объект.ШагиКонвейера.Добавить().ДействиеКонвейераОбработки = Стр.ДействиеКонвейераОбработки;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	ПередЗаписьюШагиКонвейераНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗагрузитьСписокОбъектовЗавершение(Текст, ДополнительныеПараметры) Экспорт
	
	Если Текст = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Сч=1 по СтрЧислоСтрок(Текст) Цикл
		Строка = СтрПолучитьСтроку(Текст, Сч);
		Объект.ОбъектыЗапускаИнтеграционныхТестов.Добавить().ИмяОбъекта = Строка;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗапуститьКонвейерОбработки(Команда)
	БП = ЗапуститьКонвейерОбработкиНаСервере();
	ПоказатьЗначение(Новый ОписаниеОповещения, БП);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСписокОбъектов(Команда)
	
	Текст = "";
	Продолжение = Новый ОписаниеОповещения("ЗагрузитьСписокОбъектовЗавершение", ЭтотОбъект);
	ПоказатьВводСтроки(Продолжение, Текст, "Объекты для загрузки", 0, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьМетаданные(Команда)
	ЗагрузитьМетаданныеНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЗапуститьКонвейерОбработкиНаСервере()
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("РучнойЗапуск", Истина);
	Реквизиты.Вставить("ШагиКонвейера", ШагиКонвейера.Выгрузить(ШагиКонвейера.НайтиСтроки(Новый Структура("Отметка", Истина))));
	БП = БизнесПроцессы.КонвейерОбработки.ЗапускНовогоБизнесПроцесса(Объект.Ссылка, Реквизиты);
	Возврат БП;
КонецФункции

&НаСервере
Процедура ЗагрузитьМетаданныеНаСервере()
	ЗагрузкаМетаданных.ЗагрузитьМетаданные(Объект.Ссылка);
КонецПроцедуры

#КонецОбласти


