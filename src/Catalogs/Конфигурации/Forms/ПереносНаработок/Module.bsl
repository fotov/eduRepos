
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, "Проект");
	
	Хранилище = Конфигурация.ХранилищеРазработки;
	Пользователь = Пользователи.ТекущийПользователь();
		
	РаботаСХранилищами.ОбновитьИсториюХранилища(Хранилище);
	
	УстановитьОтборДинамическимСпискам();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиНаработки(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПеренестиНаработкиНаСервере();
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ПеренестиНаработкиНаСервере()	
	
	РаботаСХранилищами.ПеренестиНаработки(Конфигурация, ВерсииХранилища());
	
КонецПроцедуры

&НаСервере
Функция ВерсииХранилища()
	
	Схема = Элементы.ИсторияХранилища.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
	Настройки = Элементы.ИсторияХранилища.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(Схема, Настройки, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	Коды = ПроцессорВывода.Вывести(ПроцессорКомпоновки).ВыгрузитьКолонку("Код");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИсторияХранилища.Ссылка КАК Версия
	|ИЗ
	|	Справочник.ИсторияХранилища КАК ИсторияХранилища
	|ГДЕ
	|	ИсторияХранилища.Владелец = &Хранилище
	|	И ИсторияХранилища.Код В(&Коды)";
	Запрос.УстановитьПараметр("Хранилище", Хранилище);
	Запрос.УстановитьПараметр("Коды", Коды);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура УстановитьОтборДинамическимСпискам()
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ИсторияХранилища.Отбор, "Владелец", Хранилище,,, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ИсторияХранилища.Отбор, "Метка", "",,, Истина);
КонецПроцедуры

#КонецОбласти

#КонецОбласти
