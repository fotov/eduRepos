
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьОтборДинамическимСпискам();
	
	URLЗадачиМетеор = Справочники.ЗадачиМетеор.URLЗадачиМетеор(Объект);
	
	ВывестиОписаниеНаСервере();
	
	РаботаСФормамиСервер.НастроитьЭлементыФормыКонфликтыИзменений(КонфликтыИзменений, Элементы.ГруппаКонфликтыИзменений, Объект.Ссылка);
	Объекты.Параметры.Элементы.Найти("ЗадачаМетеор").Использование = Истина;
	Объекты.Параметры.Элементы.Найти("ЗадачаМетеор").Значение = Объект.Ссылка;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьОтборДинамическимСпискам();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура URLЗадачиМетеорНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НачатьЗапускПриложения(Новый ОписаниеОповещения, URLЗадачиМетеор);
	
КонецПроцедуры

&НаКлиенте
Процедура idv3Открытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОбновитьДанныеИзМетеора();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьОписание(Команда)
	ОбновитьОписаниеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КонфликтыИзмененийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	РаботаСФормамиКлиент.КонфликтыИзмененийВыбор(Элемент, Поле);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВеткуГит(Команда)
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Перед выполнением команды необходимо записать элемент.");
		Возврат;
	КонецЕсли;
	КонфигурацияЗадачи = ПредопределенноеЗначение("Справочник.Конфигурации.ПустаяСсылка");
	Если ВвестиЗначение(КонфигурацияЗадачи, "Укажите конфигурацию задачи") Тогда
		СоздатьВеткуГитНаСервере(Объект.id, КонфигурацияЗадачи);
	КонецЕсли;
	ОбщегоНазначенияКлиент.СообщитьПользователю("Обработка выполнена.");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДанныеИзМетеора()
	Справочники.ЗадачиМетеор.ОбновитьДанныеИзМетеора(Объект.Ссылка);
	Прочитать();
КонецПроцедуры

#Область Прочее

&НаСервере
Процедура УстановитьОтборДинамическимСпискам()
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ИсторияЭтапов.Отбор, "ЗадачаМетеор", Объект.Ссылка,,, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ИсторияХранилища.Отбор, "ЗадачаМетеор", Объект.Ссылка,,, Истина);
КонецПроцедуры

&НаСервере
Процедура ОбновитьОписаниеНаСервере()
	ВывестиОписаниеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВывестиОписаниеНаСервере()
	ОписаниеHTML = Справочники.ЗадачиМетеор.ПолучитьПредставлениеЗадачиВHTML(Объект.Ссылка);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоздатьВеткуГитНаСервере(ЗадачаМетеорИд, КонфигурацияЗадачи)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Конфигурации.ПроектGitEDT КАК ПроектGitEDT,
		|	Конфигурации.ПроектGitEDT.Код КАК ПроектGitEDTКод,
		|	Конфигурации.ПроектGitEDT.Сервер КАК ПроектGitEDTСервер,
		|	Конфигурации.ПроектGitEDT.ИмяВеткиПоУмолчанию КАК ПроектGitEDTИмяВеткиПоУмолчанию
		|ИЗ
		|	Справочник.Конфигурации КАК Конфигурации
		|ГДЕ
		|	Конфигурации.Ссылка = &Конфигурация
		|	И НЕ Конфигурации.ПроектGitEDT.Код ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("Конфигурация", КонфигурацияЗадачи);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ВызватьИсключение "Для конфигурации на задан проект Гит";
	КонецЕсли;
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();

	РезультатСозданияВетки = РаботаСGIT.СоздатьВеткуАПИ(
		Выборка.ПроектGitEDTКод,
		ЗадачаМетеорИд,
		Выборка.ПроектGitEDTИмяВеткиПоУмолчанию,
		Выборка.ПроектGitEDTСервер);
	Если РезультатСозданияВетки.КодСостояния >= 300 Тогда
		ТекстОшибки = СтрШаблон(
				"Задача: %1
				|Код состояния: %2
				|Ответ АПИ: %3",
				ЗадачаМетеорИд,
				РезультатСозданияВетки.КодСостояния,
				РезультатСозданияВетки.ТелоОтвета);
		ЗаписьЖурналаРегистрации(
			"Работа с гит.Создание ветки",
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.ЗадачиМетеор,
			,
			ТекстОшибки);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
