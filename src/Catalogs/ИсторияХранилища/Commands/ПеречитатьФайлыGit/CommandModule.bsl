
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПеречитатьФайлыGitНаСервере(ПараметрКоманды);
	ОповеститьОбИзменении(Тип("СправочникСсылка.ИсторияХранилища"));
	
КонецПроцедуры

&НаСервере
Процедура ПеречитатьФайлыGitНаСервере(ВерсияХранилища)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗначенияРесурсов = РегистрыСведений.ХешиGitВерсийХранилища.Получить(Новый Структура("ВерсияХранилища", ВерсияХранилища));
	Хеш = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЗначенияРесурсов, "Хеш");
	об = ВерсияХранилища.ПолучитьОбъект();
	об.ФайлыGit.Загрузить(РаботаСХранилищами.ФайлыGit(об.Владелец, Хеш, ПредыдущийХэшGit(об.Владелец, об.Код)));
	об.Записать();
	
КонецПроцедуры

&НаСервере
Функция ПредыдущийХэшGit(Хранилище, ТекущийКод)
	Результат = "";
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МАКСИМУМ(ИсторияХранилища.Код) КАК Код
		|ПОМЕСТИТЬ ПредыдущийКод
		|ИЗ
		|	Справочник.ИсторияХранилища КАК ИсторияХранилища
		|ГДЕ
		|	ИсторияХранилища.Владелец = &Хранилище
		|	И ИсторияХранилища.Код < &ТекущийКод
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХешиGitВерсийХранилища.Хеш КАК ХешGit
		|ИЗ
		|	Справочник.ИсторияХранилища КАК ИсторияХранилища
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ХешиGitВерсийХранилища КАК ХешиGitВерсийХранилища
		|		ПО ХешиGitВерсийХранилища.ВерсияХранилища = ИсторияХранилища.Ссылка
		|ГДЕ
		|	ИсторияХранилища.Владелец = &Хранилище
		|	И ИсторияХранилища.Код В
		|		(ВЫБРАТЬ
		|			ПредыдущийКод.Код
		|		ИЗ
		|			ПредыдущийКод)";
	Запрос.УстановитьПараметр("Хранилище", Хранилище);
	Запрос.УстановитьПараметр("ТекущийКод", ТекущийКод);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.ХешGit;
	КонецЕсли;
	Возврат Результат;
КонецФункции
