#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытийКартыМаршрута

Процедура ПеревестиНаСледующийЭтапПриВыполнении(ТочкаМаршрутаБизнесПроцесса, Задача, Отказ)
	
	Если Не ИнтеграцияСМетеор.ПроверитьТекущийЭтапЗадачиВМетеоре(ЗадачаМетеор, Справочники.ЭтапыЗадачиМетеор.ПодключениеРасширения) Тогда
		Возврат;
	КонецЕсли;
	
	Этапы = РегистрыСведений.СледующиеЭтапыЗадачиПоТочкеМаршрута.ПолучитьЭтапыПоТочкеМаршрута(
		Бизнеспроцессы.ПодключениеРасширения.ТочкиМаршрута.ПеревестиНаСледующийЭтап,
		Согласовано);
	ИнтеграцияСМетеор.ПеревестиЗадачуНаЭтапВМетеоре(ЗадачаМетеор, Этапы);
	
КонецПроцедуры

Процедура ПодключитьРасширениеПриВыполнении(ТочкаМаршрутаБизнесПроцесса, Задача, Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	МаксимальнаяДлительностьПопыток = 300;
	Если (ТекущаяДатаСеанса() - Дата) > МаксимальнаяДлительностьПопыток Тогда
		ТекстМетеор = "Ошибка: Истекло время на попытки подключения расширения.";
		ИнтеграцияСМетеор.ОтправитьФорматированныйКомментарийВМетеор(ЗадачаМетеор, ТекстМетеор);
		Согласовано = Ложь;
		Записать();
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	СообщенияОВыполнении = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВерсииДополнений.Ссылка КАК ВерсияДополнения,
		|	ВерсииДополненийПоЗадачеИБазеСрезПоследних.База КАК База,
		|	ВерсииДополнений.Наименование КАК ВерсияДополненияПредставление,
		|	ПРЕДСТАВЛЕНИЕ(ВерсииДополненийПоЗадачеИБазеСрезПоследних.База) КАК БазаПредставление,
		|	ВерсииДополнений.ИмяОбъекта КАК Имя
		|ИЗ
		|	РегистрСведений.ЗадачиМетеорДополнений КАК ЗадачиМетеорДополнений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВерсииДополнений КАК ВерсииДополнений
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВерсииДополненийПоЗадачеИБазе.СрезПоследних(, ЗадачаМетеор = &ЗадачаМетеор) КАК ВерсииДополненийПоЗадачеИБазеСрезПоследних
		|			ПО ВерсииДополнений.Ссылка = ВерсииДополненийПоЗадачеИБазеСрезПоследних.ВерсияДополнения
		|				И (ВерсииДополненийПоЗадачеИБазеСрезПоследних.ЗадачаМетеор = &ЗадачаМетеор)
		|		ПО ЗадачиМетеорДополнений.ВерсияДополнения = ВерсииДополнений.Ссылка
		|ГДЕ
		|	ЗадачиМетеорДополнений.ЗадачаМетеор = &ЗадачаМетеор";
	
	Запрос.УстановитьПараметр("ЗадачаМетеор", ЗадачаМетеор);
	Выборка = Запрос.Выполнить().Выбрать();
	Попытка
		Пока Выборка.Следующий() Цикл
			РаботаСДополнениями.УстановитьДополнение(Выборка.База, Выборка.ВерсияДополнения);
			СообщенияОВыполнении.Добавить(СтрШаблон("Подключено %1 в базу %2", Выборка.ВерсияДополненияПредставление, Выборка.БазаПредставление));
		КонецЦикла;
		Согласовано = Истина;
		Записать();
	Исключение
		СообщенияОВыполнении.Добавить(СтрШаблон("При выполнении операции с расширениями: %1, возникли ошибки: %2", ЭтотОбъект, ОписаниеОшибки()));
		Отказ = Истина;
	КонецПопытки;
	
	Если Не Отказ Тогда
		Базы = Новый Массив;
		Выборка.Сбросить();
		Пока Выборка.Следующий() Цикл
			Базы.Добавить(Выборка.База);
		КонецЦикла;
		СостояниеДополненийВБазах = РаботаСДополнениями.ПолучитьСостояниеДополнений(
			Перечисления.ТипыДополнений.Расширение, Базы);
		ОтборБазаРасширение = Новый Структура("База, Имя, Активно", Неопределено, Неопределено, Истина);
		Выборка.Сбросить();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ОтборБазаРасширение, Выборка);
			УстановленныеРасширения = СостояниеДополненийВБазах.НайтиСтроки(ОтборБазаРасширение);
			Если Не УстановленныеРасширения.Количество() Тогда
				СообщенияОВыполнении.Добавить(СтрШаблон(
				"Ошибка: После подключения расширения %1 в базу %2 - не найдено подключенное активное расширение. (при подключении не было исключений)",
					Выборка.ВерсияДополненияПредставление, Выборка.БазаПредставление));
				Отказ = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ТекстМетеор = СтрСоединить(СообщенияОВыполнении, Символы.ПС);
	ИнтеграцияСМетеор.ОтправитьФорматированныйКомментарийВМетеор(ЗадачаМетеор, ТекстМетеор);
	Если Отказ Тогда
		// Транзакция в данном случае откатывается. Признак согласования не меняется.
		ВызватьИсключение ТекстМетеор;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийМодуля

#КонецОбласти

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
	
#КонецОбласти

#Иначе
  ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли