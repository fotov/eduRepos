
#Область ОбработчикиСобытийКартыМаршрута

Процедура ЕстьПревышениеОценкиТрудозатратПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	
	// Проверка превышения суммарной оценки и суммарных трудозатрат 
	ЧасыПоЗадаче = Справочники.ЗадачиМетеор.ПолучитьЧасыПоЗадачеМетеор(ЗадачаМетеор);
	Если Не ЧасыПоЗадаче.Количество() Тогда
		Результат = Ложь;
		Возврат;
	КонецЕсли;

	ЧасыПоЗадаче.Свернуть(, "Затрачено");
	ВсегоЗатрачено = ЧасыПоЗадаче[0].Затрачено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЗадачаМетеор", ЗадачаМетеор);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ОценкаТрудозатратПоЗадачам.Количество), 0) КАК Количество,
		|	МаксимальныйПроцентПревышенияОценкиКонтроляТрудозатрат.Значение КАК МаксимальныйПроцентПревышенияОценкиКонтроля
		|ИЗ
		|	РегистрСведений.ОценкаТрудозатратПоЗадачам КАК ОценкаТрудозатратПоЗадачам,
		|	Константа.МаксимальныйПроцентПревышенияОценкиКонтроляТрудозатрат КАК МаксимальныйПроцентПревышенияОценкиКонтроляТрудозатрат
		|ГДЕ
		|	ОценкаТрудозатратПоЗадачам.ЗадачаМетеор = &ЗадачаМетеор
		|
		|СГРУППИРОВАТЬ ПО
		|	МаксимальныйПроцентПревышенияОценкиКонтроляТрудозатрат.Значение";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		// Не найдены оценки. Предположительно, программное движение процесса. Значит пропускаем шаг согласования.
		Результат = Ложь;
		Возврат;
	КонецЕсли;
	
	ПроцентПревышения = (ВсегоЗатрачено - Выборка.Количество) / Выборка.Количество * 100;
	Результат = ПроцентПревышения >= Выборка.МаксимальныйПроцентПревышенияОценкиКонтроля;	
	
КонецПроцедуры

Процедура ОценкаТрудозатратРазработкиПриСозданииЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, Отказ)
	ЗаполнитьКомментарийЗадачиИзНаименования(ФормируемыеЗадачи);
КонецПроцедуры

Процедура ОценкаТрудозатратАналитикиПриСозданииЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, Отказ)
	ЗаполнитьКомментарийЗадачиИзНаименования(ФормируемыеЗадачи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийМодуля

#КонецОбласти

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьКомментарийЗадачиИзНаименования(ФормируемыеЗадачи)
	// Для отображения в списке в обработке "Согласование заявок"
	Для Каждого ФормируемаяЗадача Из ФормируемыеЗадачи Цикл
		ФормируемаяЗадача.Комментарий = ФормируемаяЗадача.Наименование;		
	КонецЦикла;
КонецПроцедуры


#КонецОбласти



