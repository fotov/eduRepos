
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	ЭтоНаборУдаления = Количество() = 0; 
	Если ЭтоНаборУдаления Тогда
		ДополнительныеСвойства.Вставить("БылоУдаление");
	КонецЕсли;
	
	Если ЭтоНаборУдаления Или Не ДополнительныеСвойства.Свойство("БылоУдаление") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	РезультатыСинтаксическойПроверки.ПровереноBSLLS КАК ПровереноBSLLS,
			|	РезультатыСинтаксическойПроверки.ПровереноАПК КАК ПровереноАПК
			|ИЗ
			|	РегистрСведений.РезультатыСинтаксическойПроверки КАК РезультатыСинтаксическойПроверки
			|ГДЕ
			|	РезультатыСинтаксическойПроверки.ОбъектПроверки = &ОбъектПроверки";
		Запрос.УстановитьПараметр("ОбъектПроверки", Отбор.ОбъектПроверки.Значение);
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			Если Выборка.ПровереноBSLLS Тогда
				ДополнительныеСвойства.Вставить("БылоПровереноBSLLS");
			КонецЕсли;
			Если Выборка.ПровереноАПК Тогда
				ДополнительныеСвойства.Вставить("БылоПровереноАПК");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	Если Количество() = 1 И Не ДополнительныеСвойства.Свойство("БылоПровереноBSLLS") И ЭтотОбъект[0].ПровереноBSLLS Тогда
		Менеджер = РегистрыСведений.ОчередьУведомленийСинтаксическойПроверки.СоздатьМенеджерЗаписи();
		Менеджер.Период = ТекущаяДатаСеанса();
		Менеджер.ОбъектПроверки = ЭтотОбъект[0].ОбъектПроверки;
		Менеджер.ЭтоПроверкаАПК = Ложь;
		Менеджер.Статус = Перечисления.СтатусыОперацийУведомлений.ВОчереди;
		Менеджер.Записать(Ложь);
	КонецЕсли;	
	Если Количество() = 1 И Не ДополнительныеСвойства.Свойство("БылоПровереноАПК") И ЭтотОбъект[0].ПровереноАПК Тогда
		Менеджер = РегистрыСведений.ОчередьУведомленийСинтаксическойПроверки.СоздатьМенеджерЗаписи();
		Менеджер.Период = ТекущаяДатаСеанса();
		Менеджер.ОбъектПроверки = ЭтотОбъект[0].ОбъектПроверки;
		Менеджер.ЭтоПроверкаАПК = Истина;
		Менеджер.Статус = Перечисления.СтатусыОперацийУведомлений.ВОчереди;
		Менеджер.Записать(Ложь);
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#Иначе
  ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли