#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура Отразить(ЗадачаМетеор, Этап, Исполнитель, Тип) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	мз = РегистрыСведений.ТекущийЭтапЗадачиМетеор.СоздатьМенеджерЗаписи();
	мз.ЗадачаМетеор = ЗадачаМетеор;
	мз.Этап = Этап;
	мз.Исполнитель = Исполнитель;
	мз.Тип = Тип;
	мз.Записать();
	
КонецПроцедуры

Функция ДанныеЭтапа(ЗадачаМетеор) Экспорт
	
	ДанныеЭтапа = Новый Структура;
	ДанныеЭтапа.Вставить("Этап", Справочники.ЭтапыЗадачиМетеор.ПустаяСсылка());
	ДанныеЭтапа.Вставить("Тип", Справочники.ТипыЗадачиМетеор.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТекущийЭтапЗадачиМетеор.Этап КАК Этап,
	|	ТекущийЭтапЗадачиМетеор.Тип КАК Тип
	|ИЗ
	|	РегистрСведений.ТекущийЭтапЗадачиМетеор КАК ТекущийЭтапЗадачиМетеор
	|ГДЕ
	|	ТекущийЭтапЗадачиМетеор.ЗадачаМетеор = &ЗадачаМетеор";
	Запрос.УстановитьПараметр("ЗадачаМетеор", ЗадачаМетеор);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ДанныеЭтапа, Выборка);
	КонецЕсли;
	Возврат ДанныеЭтапа;
	
КонецФункции

Процедура Синхронизировать() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИсторияЭтаповЗадачиМетеор.ЗадачаМетеор КАК ЗадачаМетеор,
		|	МАКСИМУМ(ИсторияЭтаповЗадачиМетеор.Дата) КАК Дата
		|ПОМЕСТИТЬ ДатыЭтапов
		|ИЗ
		|	РегистрСведений.ИсторияЭтаповЗадачиМетеор КАК ИсторияЭтаповЗадачиМетеор
		|
		|СГРУППИРОВАТЬ ПО
		|	ИсторияЭтаповЗадачиМетеор.ЗадачаМетеор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИсторияЭтаповЗадачиМетеор.ЗадачаМетеор КАК ЗадачаМетеор,
		|	ИсторияЭтаповЗадачиМетеор.Дата КАК Дата,
		|	ИсторияЭтаповЗадачиМетеор.Этап КАК Этап,
		|	ИсторияЭтаповЗадачиМетеор.Исполнитель КАК Исполнитель,
		|	ИсторияЭтаповЗадачиМетеор.Тип КАК Тип,
		|	ИсторияЭтаповЗадачиМетеор.Обработано КАК Обработано,
		|	ИсторияЭтаповЗадачиМетеор.стрИсполнитель КАК стрИсполнитель,
		|	ИсторияЭтаповЗадачиМетеор.Комментарий КАК Комментарий
		|ИЗ
		|	РегистрСведений.ИсторияЭтаповЗадачиМетеор КАК ИсторияЭтаповЗадачиМетеор
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыЭтапов КАК ДатыЭтапов
		|		ПО ИсторияЭтаповЗадачиМетеор.ЗадачаМетеор = ДатыЭтапов.ЗадачаМетеор
		|			И ИсторияЭтаповЗадачиМетеор.Дата = ДатыЭтапов.Дата
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТекущийЭтапЗадачиМетеор КАК ТекущийЭтапЗадачиМетеор
		|		ПО ИсторияЭтаповЗадачиМетеор.ЗадачаМетеор = ТекущийЭтапЗадачиМетеор.ЗадачаМетеор
		|ГДЕ
		|	ИсторияЭтаповЗадачиМетеор.Этап <> ТекущийЭтапЗадачиМетеор.Этап";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Сообщить(Выборка.ЗадачаМетеор);
		РегистрыСведений.ТекущийЭтапЗадачиМетеор.Отразить(Выборка.ЗадачаМетеор, Выборка.Этап, Выборка.Исполнитель, Выборка.Тип);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#КонецЕсли
