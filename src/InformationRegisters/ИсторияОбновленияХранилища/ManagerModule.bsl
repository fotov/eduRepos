#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура Отразить(Хранилище, Дата) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	мз = РегистрыСведений.ИсторияОбновленияХранилища.СоздатьМенеджерЗаписи();
	мз.Хранилище = Хранилище;
	мз.Дата = Дата;
	мз.Записать();
	
КонецПроцедуры

Функция ПоследняяДатаОбновленияХранилищ(Дата = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ИсторияОбновленияХранилища.Дата) КАК Дата,
	|	ИсторияОбновленияХранилища.Хранилище КАК Хранилище
	|ПОМЕСТИТЬ ПоследниеДаты
	|ИЗ
	|	РегистрСведений.ИсторияОбновленияХранилища КАК ИсторияОбновленияХранилища
	|ГДЕ
	|	ИсторияОбновленияХранилища.Хранилище В
	|			(ВЫБРАТЬ
	|				Справочник.Конфигурации.ХранилищеРазработки
	|			ИЗ
	|				Справочник.Конфигурации)
	|	И &ОтборПоДате
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсторияОбновленияХранилища.Хранилище
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ПоследниеДаты.Дата) КАК Дата
	|ИЗ
	|	ПоследниеДаты КАК ПоследниеДаты";
	Запрос.УстановитьПараметр("Дата", Дата);
	
	Если ЗначениеЗаполнено(Дата) Тогда
		Текст_ОтборПоДате = "Дата < &Дата";
	Иначе
		Текст_ОтборПоДате = "ИСТИНА";
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоДате", Текст_ОтборПоДате);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПоследняяДатаОбновления = Выборка.Дата;
	Иначе
		ПоследняяДатаОбновления = '00010101';
	КонецЕсли;
	Возврат ПоследняяДатаОбновления;
	
КонецФункции

Функция ПредпоследняяДатаОбновленияХранилищ() Экспорт
	
	Дата = ПоследняяДатаОбновленияХранилищ();
	Возврат ПоследняяДатаОбновленияХранилищ(Дата);
	
КонецФункции

#КонецОбласти

#КонецЕсли
