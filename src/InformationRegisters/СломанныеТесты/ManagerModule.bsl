#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура Отразить(Коммит) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПройденныеТесты.Коммит КАК Коммит,
	|	ПройденныеТесты.Тест КАК Тест,
	|	ПройденныеТесты.ТекстОшибки КАК ТекстОшибки
	|ИЗ
	|	РегистрСведений.ПройденныеТесты КАК ПройденныеТесты
	|ГДЕ
	|	ПройденныеТесты.Коммит = &Коммит
	|	И НЕ ПройденныеТесты.ТестПройден
	|	И НЕ ПройденныеТесты.Тест В
	|				(ВЫБРАТЬ
	|					РегистрСведений.СломанныеТесты.Тест
	|				ИЗ
	|					РегистрСведений.СломанныеТесты
	|				ГДЕ
	|					НЕ РегистрСведений.СломанныеТесты.Исправлен)";
	Запрос.УстановитьПараметр("Коммит", Коммит);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		мз = РегистрыСведений.СломанныеТесты.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(мз, Выборка);
		мз.Записать();
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СломанныеТесты.Коммит КАК Коммит,
	|	СломанныеТесты.Тест КАК Тест
	|ИЗ
	|	РегистрСведений.СломанныеТесты КАК СломанныеТесты
	|ГДЕ
	|	НЕ СломанныеТесты.Исправлен
	|	И СломанныеТесты.Тест В
	|			(ВЫБРАТЬ
	|				ПройденныеТесты.Тест КАК Тест
	|			ИЗ
	|				РегистрСведений.ПройденныеТесты КАК ПройденныеТесты
	|			ГДЕ
	|				ПройденныеТесты.Коммит = &Коммит
	|				И ПройденныеТесты.ТестПройден)";
	Запрос.УстановитьПараметр("Коммит", Коммит);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		мз = РегистрыСведений.СломанныеТесты.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(мз, Выборка);
		мз.Прочитать();
		Если мз.Выбран() Тогда
			мз.КоммитИсправления = Коммит;
			мз.Исправлен = Истина;
			мз.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ОшибокАвтотесты(Коммит) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(1), 0) КАК ОшибокАвтотесты
	|ИЗ
	|	РегистрСведений.СломанныеТесты КАК СломанныеТесты
	|ГДЕ
	|	СломанныеТесты.Коммит = &Коммит";
	Запрос.УстановитьПараметр("Коммит", Коммит);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ОшибокАвтотесты = Выборка.ОшибокАвтотесты;
	Иначе
		ОшибокАвтотесты = 0;
	КонецЕсли;
	Возврат ОшибокАвтотесты;
	
КонецФункции

#КонецОбласти

#КонецЕсли
