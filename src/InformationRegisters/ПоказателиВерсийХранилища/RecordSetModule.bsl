
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	ЭтоНаборУдаления = Количество() = 0; 
	Если ЭтоНаборУдаления Тогда
		ДополнительныеСвойства.Вставить("БылоУдаление");
	КонецЕсли;
	
	Если ЭтоНаборУдаления Или Не ДополнительныеСвойства.Свойство("БылоУдаление") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ИСТИНА КАК АвтотестыЗагружены
			|ИЗ
			|	РегистрСведений.ПоказателиВерсийХранилища КАК ПоказателиВерсийХранилища
			|ГДЕ
			|	ПоказателиВерсийХранилища.ВерсияХранилища = &ВерсияХранилища
			|	И ПоказателиВерсийХранилища.ДанныеЗагружены_Автотесты";
		Запрос.УстановитьПараметр("ВерсияХранилища", Отбор.ВерсияХранилища.Значение);
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			ДополнительныеСвойства.Вставить("АвтотестыБылиЗагружены");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	Если Количество() = 1 И Не ДополнительныеСвойства.Свойство("АвтотестыБылиЗагружены") И ЭтотОбъект[0].ДанныеЗагружены_Автотесты Тогда
		ОтправитьПисьмоСОшибкамиАвтотестов(); 
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОтправитьПисьмоСОшибкамиАвтотестов()
	
	ПоказателиАвтотестов = ЭтотОбъект[0];
	
	Если Не ЗначениеЗаполнено(ПоказателиАвтотестов.ОшибокАвтотесты) Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияРесурсов = РегистрыСведений.ЗадачиМетеорВерсийХранилища.Получить(Новый Структура("ВерсияХранилища", ПоказателиАвтотестов.ВерсияХранилища));
	ЗадачаМетеор = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЗначенияРесурсов, "ЗадачаМетеор");
	
	МассивКоммитов = Новый Массив;
	МассивКоммитов.Добавить(ПоказателиАвтотестов.ВерсияХранилища);
	
	Адреса = Справочники.ИсторияХранилища.АдресаАвторовКоммитов(МассивКоммитов);
	Аналитик = РегистрыСведений.ИсторияЭтаповЗадачиМетеор.ИсполнительЭтапа(ЗадачаМетеор, Справочники.ЭтапыЗадачиМетеор.АналитическаяПроработка);
	Если ЗначениеЗаполнено(Аналитик.email) Тогда
		Адреса.Добавить(Аналитик.email);
	КонецЕсли;
	Ведущий = РегистрыСведений.ИсторияЭтаповЗадачиМетеор.ИсполнительЭтапа(ЗадачаМетеор, Справочники.ЭтапыЗадачиМетеор.СогласоватьАрхитектуру);
	Если ЗначениеЗаполнено(Ведущий.email) Тогда
		Адреса.Добавить(Ведущий.email);
	КонецЕсли;
	Разработчик = РегистрыСведений.ИсторияЭтаповЗадачиМетеор.ИсполнительЭтапа(ЗадачаМетеор, Справочники.ЭтапыЗадачиМетеор.ВыполнитьОсновнуюРаботуПоЗаданию);
	Если ЗначениеЗаполнено(Разработчик.email) Тогда
		Адреса.Добавить(Разработчик.email);
	КонецЕсли;
	Для Каждого Пользователь Из Справочники.Пользователи.ПользователиРоли(Справочники.РолиИсполнителей.ВладелецСистемы) Цикл
		Адреса.Добавить(Пользователь.email);
	КонецЦикла;
	Для Каждого Пользователь Из Справочники.Пользователи.ПользователиРоли(Справочники.РолиИсполнителей.Тестирование) Цикл
		Адреса.Добавить(Пользователь.email);
	КонецЦикла;
	Адреса = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Адреса);
	АдресПолучателя = СтрСоединить(Адреса, ";");
	Если НЕ ЗначениеЗаполнено(АдресПолучателя) Тогда
		Возврат;
	КонецЕсли;
	
	URLAllure = Справочники.ИсторияХранилища.URLAllure(ЭтотОбъект);
	URLJUnit  = Справочники.ИсторияХранилища.URLJUnit(ЭтотОбъект);
	
	ТемаПисьма = СтрШаблон("Ошибки автотестов по коммиту №%1", ПоказателиАвтотестов.ВерсияХранилища);
	
	ШаблонПисьма = "<!doctype html><html><head></head><body>%1</body></html>";
	
	СтрокиПисьма = Новый Массив;
	СтрокиПисьма.Добавить("<p>Ошибки автотестов</p>");
	СтрокиПисьма.Добавить(СтрШаблон("<p><a href=""%1"">Allure</a></p>", URLAllure));
	СтрокиПисьма.Добавить(СтрШаблон("<p><a href=""%1"">JUnit</a></p>", URLJUnit));
	СтрокиПисьма.Добавить(СтрШаблон("<p>Всего ошибок %1</p>", ПоказателиАвтотестов.ОшибокАвтотесты));
	
	СломанныеТесты = "";
	нз = РегистрыСведений.СломанныеТесты.СоздатьНаборЗаписей();
	нз.Отбор.Коммит.Установить(ПоказателиАвтотестов.ВерсияХранилища);
	нз.Прочитать();
	Для Каждого Запись Из нз Цикл 
		СломанныеТесты = СломанныеТесты + СтрШаблон("<li>%1/%2. По причине: %3</li>", Запись.Тест.Наименование, Запись.Тест.Класс, Запись.ТекстОшибки);
	КонецЦикла;
	СтрокиПисьма.Добавить(СтрШаблон("<ul>%1</ul>", СломанныеТесты));
	
	ТелоПисьма = СтрШаблон(ШаблонПисьма, СтрСоединить(СтрокиПисьма, Символы.ПС));
	
	ПараметрыПисьма = Новый Структура;
	ПараметрыПисьма.Вставить("Получатели", АдресПолучателя);
	ПараметрыПисьма.Вставить("Тема", ТемаПисьма);
	ПараметрыПисьма.Вставить("Тело", ТелоПисьма);
	Документы.ЭлектронноеПисьмо.СоздатьПисьмо(ПараметрыПисьма);
	
КонецПроцедуры

#КонецОбласти

#Иначе
  ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли