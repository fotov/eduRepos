#Область ПрограммныйИнтерфейс

Функция ДобавитьСобытие(ИнформационнаяБаза, Операция, ДопПараметры) Экспорт
	Запись = РегистрыСведений.ИсторияЗапросовВерсийХранилища.СоздатьМенеджерЗаписи();
	Если ДопПараметры<>Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Запись, ДопПараметры);
	КонецЕсли;
	Запись.ДатаЗаписиУниверсальная = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Запись.Период = ТекущаяДатаСеанса();
	Если Не ЗначениеЗаполнено(Запись.Пользователь) Тогда
		Запись.Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Запись.ИнформационнаяБаза = ИнформационнаяБаза;
	Запись.Операция = Операция;
	
	Запись.Записать();
	Возврат Запись;
КонецФункции

Процедура ЗакончитьСобытие(Рег, Комментарий="", Ошибка=Ложь) Экспорт
	Рег.Длительность = ТекущаяДатаСеанса() - Рег.Период;
	Рег.ВПроцессе = Ложь;
	Рег.Ошибка = Ошибка;
	Рег.ТекстОшибки = Комментарий;
	Рег.Записать();
КонецПроцедуры	

Процедура ОчисткаУстаревшихДанных(СколькоДнейОставить = 14) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Рег.ВерсияХранилища КАК ВерсияХранилища,
		|	Рег.ДатаЗаписиУниверсальная КАК ДатаЗаписиУниверсальная
		|ИЗ
		|	РегистрСведений.ИсторияЗапросовВерсийХранилища КАК Рег
		|ГДЕ
		|	Рег.Период < &День
		|
		|УПОРЯДОЧИТЬ ПО
		|	Рег.ДатаЗаписиУниверсальная";
	Запрос.УстановитьПараметр("День", НачалоДня(ТекущаяДата()-СколькоДнейОставить*24*3600));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Ластик = РегистрыСведений.ИсторияЗапросовВерсийХранилища.СоздатьНаборЗаписей();
		Ластик.Отбор.ВерсияХранилища.Установить(Выборка.ВерсияХранилища);
		Ластик.Отбор.ДатаЗаписиУниверсальная.Установить(Выборка.ДатаЗаписиУниверсальная);
		Ластик.Записать();
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
