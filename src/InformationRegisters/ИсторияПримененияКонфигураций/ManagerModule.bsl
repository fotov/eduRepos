#Область ПрограммныйИнтерфейс

// Процедура - Отправить оповещения обновлений в Метеор
//
Процедура ОтправитьОповещенияОбновленийВМетеор() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИсторияПримененияКонфигурацийСрезПоследних.Период КАК Период,
		|	ИсторияПримененияКонфигурацийСрезПоследних.Хранилище КАК Хранилище,
		|	ИсторияПримененияКонфигурацийСрезПоследних.База1С КАК База1С,
		|	ИсторияПримененияКонфигурацийСрезПоследних.Коммит.Код КАК КоммитКод
		|ПОМЕСТИТЬ ПоследниеОтправленные
		|ИЗ
		|	РегистрСведений.ИсторияПримененияКонфигураций.СрезПоследних(, ОповещениеОтправлено) КАК ИсторияПримененияКонфигурацийСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	""Уведомления периодически отправляются"" КАК Пояснение,
		|	МАКСИМУМ(ИсторияПримененияКонфигураций.Период) КАК Период,
		|	ИсторияПримененияКонфигураций.Хранилище КАК Хранилище,
		|	ИсторияПримененияКонфигураций.База1С КАК База1С,
		|	МИНИМУМ(ПоследниеОтправленные.КоммитКод) КАК НомерКоммитаC,
		|	МАКСИМУМ(ИсторияПримененияКонфигураций.Коммит.Код) КАК НомерКоммитаПо
		|ПОМЕСТИТЬ Неотправленные
		|ИЗ
		|	РегистрСведений.ИсторияПримененияКонфигураций КАК ИсторияПримененияКонфигураций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоследниеОтправленные КАК ПоследниеОтправленные
		|		ПО ИсторияПримененияКонфигураций.Хранилище = ПоследниеОтправленные.Хранилище
		|			И ИсторияПримененияКонфигураций.База1С = ПоследниеОтправленные.База1С
		|			И ИсторияПримененияКонфигураций.Период > ПоследниеОтправленные.Период
		|			И ИсторияПримененияКонфигураций.Коммит.Код >= ПоследниеОтправленные.КоммитКод
		|ГДЕ
		|	ИсторияПримененияКонфигураций.ОповещениеОтправлено = ЛОЖЬ
		|
		|СГРУППИРОВАТЬ ПО
		|	ИсторияПримененияКонфигураций.Хранилище,
		|	ИсторияПримененияКонфигураций.База1С
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	""Ни разу не было отправлено уведомление"",
		|	ИсторияПримененияКонфигураций.Период,
		|	ИсторияПримененияКонфигураций.Хранилище,
		|	ИсторияПримененияКонфигураций.База1С,
		|	ИсторияПримененияКонфигураций.Коммит.Код - 1,
		|	ИсторияПримененияКонфигураций.Коммит.Код
		|ИЗ
		|	РегистрСведений.ИсторияПримененияКонфигураций.СрезПоследних(, ) КАК ИсторияПримененияКонфигураций
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПоследниеОтправленные КАК ПоследниеОтправленные
		|		ПО ИсторияПримененияКонфигураций.Хранилище = ПоследниеОтправленные.Хранилище
		|			И ИсторияПримененияКонфигураций.База1С = ПоследниеОтправленные.База1С
		|ГДЕ
		|	ИсторияПримененияКонфигураций.ОповещениеОтправлено = ЛОЖЬ
		|	И ПоследниеОтправленные.Период ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 500
		|	ИсторияХранилища.Владелец КАК Хранилище,
		|	ИсторияХранилища.Ссылка КАК Коммит,
		|	ИсторияХранилища.Ссылка.Код КАК КоммитКод,
		|	ЗадачиМетеорВерсийХранилища.ЗадачаМетеор КАК ЗадачаМетеор,
		|	Неотправленные.База1С.НаименованиеМетеор КАК База1С,
		|	Неотправленные.НомерКоммитаC КАК НомерКоммитаC,
		|	Неотправленные.НомерКоммитаПо КАК НомерКоммитаПо
		|ПОМЕСТИТЬ ЗадачиМетеор
		|ИЗ
		|	РегистрСведений.ЗадачиМетеорВерсийХранилища КАК ЗадачиМетеорВерсийХранилища
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИсторияХранилища КАК ИсторияХранилища
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Неотправленные КАК Неотправленные
		|			ПО ИсторияХранилища.Владелец = Неотправленные.Хранилище
		|				И ИсторияХранилища.Код > Неотправленные.НомерКоммитаC
		|				И ИсторияХранилища.Код <= Неотправленные.НомерКоммитаПо
		|		ПО ЗадачиМетеорВерсийХранилища.ВерсияХранилища = ИсторияХранилища.Ссылка
		|			И (НЕ ЗадачиМетеорВерсийХранилища.ЗадачаМетеор = ЗНАЧЕНИЕ(Справочник.ЗадачиМетеор.ПустаяСсылка))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Неотправленные.База1С.НаименованиеМетеор,
		|	Хранилище,
		|	КоммитКод
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗадачиМетеор.ЗадачаМетеор КАК ЗадачаМетеор,
		|	ЗадачиМетеор.База1С КАК База1С
		|ИЗ
		|	ЗадачиМетеор КАК ЗадачиМетеор
		|ИТОГИ ПО
		|	ЗадачаМетеор";
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ВыборкаЗадачи = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЗадачи.Следующий() Цикл
		ВыборкаБазы = ВыборкаЗадачи.Выбрать();
		ТекстОповещения = "Изменения применены в";
		Если ВыборкаБазы.Количество() = 1 Тогда
			ТекстОповещения = ТекстОповещения + " рабочей базе";
			ВыборкаБазы.Следующий();
			Если ЗначениеЗаполнено(ВыборкаБазы.База1С) Тогда
				ТекстОповещения = ТекстОповещения + " " + ВыборкаБазы.База1С;
			КонецЕсли;
		Иначе
			ТекстОповещения = ТекстОповещения + " рабочих базах";
			Базы = Новый Массив;
			Пока ВыборкаБазы.Следующий() Цикл
				Если ЗначениеЗаполнено(ВыборкаБазы.База1С) Тогда
					Базы.Добавить(ВыборкаБазы.База1С);
				КонецЕсли;
			КонецЦикла;
			ТекстОповещения = ТекстОповещения + " " + СтрСоединить(Базы, ", ");
		КонецЕсли;
		Сообщить(СтрШаблон("%1 %2", ВыборкаЗадачи.ЗадачаМетеор, ТекстОповещения));
		ИнтеграцияСМетеор.ОтправитьФорматированныйКомментарийВМетеор(ВыборкаЗадачи.ЗадачаМетеор, ТекстОповещения);
	КонецЦикла;
	
	ВыборкаОтметки = Запрос.МенеджерВременныхТаблиц.Таблицы.Найти("Неотправленные").ПолучитьДанные().Выбрать();
	Пока ВыборкаОтметки.Следующий() Цикл
		Запись = РегистрыСведений.ИсторияПримененияКонфигураций.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, ВыборкаОтметки);
		Запись.Прочитать();
		Запись.ОповещениеОтправлено = Истина;
		Запись.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Функция - Зарегистрировать обновление ИБ
//
// Параметры:
//  СтрокаХранилище		 - Строка	 - например "fin_prod"
//  НомерКоммита		 - Число	 - например 19345
//  СтрокаБаза1С		 - Строка	 - можно оставить пустым
//  Комментарий			 - Строка	 - можно оставить пустым
//  СтрокаОтветственный	 - Строка	 - можно оставить пустым
// 
// Возвращаемое значение:
//   - Истина, если успешно, либо Исключение с описанием ошибки
//
Функция ЗарегистрироватьОбновлениеИБ(СтрокаХранилище, НомерКоммита, СтрокаБаза1С, Комментарий, СтрокаОтветственный) Экспорт
	
	Запись = РегистрыСведений.ИсторияПримененияКонфигураций.СоздатьМенеджерЗаписи();
	Запись.Период = ТекущаяДатаСеанса();
	
	Запись.Хранилище = Справочники.Хранилища.НайтиПоНаименованию(СтрокаХранилище);
	Если Не ЗначениеЗаполнено(Запись.Хранилище) Тогда
		ТекстОшибки = СтрШаблон("Не найдено хранилище ""%1"". Возможные варианты: %2", СтрокаХранилище, ВозможныеЗначенияХранилища());
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Запись.Коммит = Справочники.ИсторияХранилища.НайтиПоКоду(НомерКоммита, Истина, , Запись.Хранилище);
	Если Не ЗначениеЗаполнено(Запись.Коммит) Тогда
		ТекстОшибки = СтрШаблон("Не найден коммит ""%1"" для хранилища ""%2""", НомерКоммита, Запись.Хранилище);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаБаза1С) Тогда
		Запись.База1С = Справочники.Базы1С.НайтиПоРеквизиту("НаименованиеМетеор", СтрокаБаза1С);
		Если Не ЗначениеЗаполнено(Запись.База1С) Тогда
			ТекстОшибки = СтрШаблон("Не найдена база 1С ""%1"". Возможные варианты: %2", СтрокаБаза1С, ВозможныеЗначенияБаза1С(Запись.Хранилище));
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;
		Если ЗначениеЗаполнено(Запись.База1С.Владелец.ХранилищеОбновления) И Запись.База1С.Владелец.ХранилищеОбновления <> Запись.Хранилище Тогда
			ВызватьИсключение СтрШаблон("Хранилище базы не соотвествует переданному хранилищу (%1, %2)", Запись.Хранилище, Запись.База1С.Владелец.ХранилищеОбновления);
		КонецЕсли;
	КонецЕсли;
	
	Запись.Комментарий = Комментарий;
	
	Если ЗначениеЗаполнено(СтрокаОтветственный) Тогда
		Запись.Ответственный = Справочники.Пользователи.НайтиПоКоду(СтрокаОтветственный);
		Если Не ЗначениеЗаполнено(Запись.Ответственный) Тогда
			Запись.Ответственный = Справочники.Пользователи.НайтиПоНаименованию(СтрокаОтветственный);
		КонецЕсли;
	КонецЕсли;
	Запись.Записать();
	
	Возврат Истина;
	
КонецФункции

// Функция - Получить хранилища
// 
// Возвращаемое значение:
//   - Массив наименований хранилищ из Репос
//
Функция ПолучитьХранилища() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Конфигурации.ХранилищеОбновления.Наименование КАК Хранилище
		|ИЗ
		|	Справочник.Конфигурации КАК Конфигурации
		|ГДЕ
		|	Конфигурации.ХранилищеОбновления.БазовоеХранилище = ЗНАЧЕНИЕ(Справочник.Хранилища.ПустаяСсылка)
		|	И НЕ Конфигурации.ПометкаУдаления
		|	И НЕ Конфигурации.ХранилищеОбновления.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Хранилище";
	ТЗ = Запрос.Выполнить().Выгрузить();
	Возврат ТЗ.ВыгрузитьКолонку(0);
КонецФункции

// Функция - Получить базы1 С
//
// Параметры:
//  Хранилище	 - Строка	 - Наименование хранилища
// 
// Возвращаемое значение:
//   - Массив наименований баз 1С из Репос для указанного хранилища
//
Функция ПолучитьБазы1С(Хранилище) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Спр.НаименованиеМетеор КАК Наименование
		|ИЗ
		|	Справочник.Базы1С КАК Спр
		|ГДЕ
		|	Спр.Владелец.ХранилищеОбновления.Наименование = &Хранилище
		|	И Спр.НаименованиеМетеор <> """"
		|	И Спр.ПометкаУдаления = ЛОЖЬ";
	Запрос.УстановитьПараметр("Хранилище", Хранилище);
	ТЗ = Запрос.Выполнить().Выгрузить();
	Возврат ТЗ.ВыгрузитьКолонку(0);
КонецФункции

// Функция - Получить последний номер коммита
//
// Параметры:
//  Хранилище	 - Строка	 - Наименование хранилища
// 
// Возвращаемое значение:
//   - Число
//
Функция ПолучитьПоследнийНомерКоммита(Хранилище) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МАКСИМУМ(ИсторияХранилища.Код) КАК НомерКоммита
		|ИЗ
		|	Справочник.ИсторияХранилища КАК ИсторияХранилища
		|ГДЕ
		|	&Хранилище В (ИсторияХранилища.Владелец.Наименование, ИсторияХранилища.Владелец.БазовоеХранилище.Наименование)";
	Запрос.УстановитьПараметр("Хранилище", Хранилище);
	ТЗ = Запрос.Выполнить().Выгрузить();
	Если ТЗ.Количество() Тогда
		Возврат ТЗ[0].НомерКоммита;
	Иначе
		Возврат 0;
	КонецЕсли;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВозможныеЗначенияХранилища()
	Возврат СтрСоединить(ПолучитьХранилища(), ", ");
КонецФункции

Функция ВозможныеЗначенияБаза1С(Хранилище)
	Возврат СтрСоединить(ПолучитьБазы1С(Хранилище), ", ");
КонецФункции

#КонецОбласти
