#Если Сервер Или ТолстыйКлиентОбычноеПриложение  Тогда
	
#Область ПрограммныйИнтерфейс

// Записывает значение (или значения) настройки в регистр
//
// Параметры:
//   Настройка 	- ПланВидовХарактеристикСсылка.НастройкиПрограммы - настройка, значение которой необходимо записать
//   МассивЗначений 	- Характеристика.НастройкиПрограммы, Массив - одно или массив значений (если для настройки доступен список значений) для записи
//
Процедура ЗаписатьЗначениеНастройки(Настройка, Знач МассивЗначений) Экспорт
	Если НЕ ЗначениеЗаполнено(Настройка) Тогда
		Возврат;
	КонецЕсли; 
	
	Если ТипЗнч(МассивЗначений) <> Тип("Массив") Тогда
		МассивЗначений = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(МассивЗначений);
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ЗначенияНастроекПрограммы.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Настройка.Установить(Настройка);
	
	НомерЗначения = 0;
	Для Каждого Значение Из МассивЗначений Цикл
		НомерЗначения = НомерЗначения + 1;
		
		Запись = НаборЗаписей.Добавить();
		Запись.Настройка = Настройка;
		Запись.НомерЗначения = НомерЗначения;
		Запись.Значение = Значение;
	КонецЦикла;
	
	НаборЗаписей.Записать();
КонецПроцедуры

// Описание функции:
//  Функция возвращает значение настройки программы из регистра
//
// Параметры:
//  Настройка			 - ПланВидовХарактеристикСсылка.НастройкиПрограммы	 - Ссылка на настройку (или наименование)
//  ЗначениеПоУмолчанию	 - Произвольный	 - Возвращаемое значение, если настройка не установлена
//  ВернутьМассив		 - Булево - Вернуть результат как массив
// 
// Возвращаемое значение:
//   - Значение настройки
//
Функция ПолучитьЗначениеНастройки(Настройка, ЗначениеПоУмолчанию = Неопределено, ВернутьМассив = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗначенияНастроекПрограммы.Значение КАК Значение
		|ИЗ
		|	РегистрСведений.ЗначенияНастроекПрограммы КАК ЗначенияНастроекПрограммы
		|ГДЕ
		|	ЗначенияНастроекПрограммы.Настройка = &Настройка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗначенияНастроекПрограммы.Значение
		|ИЗ
		|	РегистрСведений.ЗначенияНастроекПрограммы КАК ЗначенияНастроекПрограммы
		|ГДЕ
		|	ЗначенияНастроекПрограммы.Настройка.Наименование = &Настройка";
	
	Запрос.УстановитьПараметр("Настройка", Настройка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если ВернутьМассив Тогда
		Массив = Новый Массив;
		Пока Выборка.Следующий() Цикл
			Массив.Добавить(Выборка.Значение);
		КонецЦикла;
		
		Возврат Массив;
	Иначе
		Если Выборка.Следующий()  Тогда
			Возврат Выборка.Значение;
		Иначе
			Возврат ЗначениеПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции		

// Функция - Получить настройки программы иерархически
//
// Параметры:
//  Настройка			 - ПланВидовХарактеристикСсылка.НастройкиПрограммы	 - Ссылка на группу настроек
// 
// Возвращаемое значение:
//   - Структура
//
Функция ПолучитьНастройкиПрограммы(Настройка) Экспорт
	
	НастройкиПрограммы = Новый Структура;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗначенияНастроекПрограммы.Значение КАК Значение,
		|	ЗначенияНастроекПрограммы.Настройка.ИмяПредопределенныхДанных КАК Настройка
		|ИЗ
		|	РегистрСведений.ЗначенияНастроекПрограммы КАК ЗначенияНастроекПрограммы
		|ГДЕ
		|	ЗначенияНастроекПрограммы.Настройка В ИЕРАРХИИ(&Настройка)
		|	И ЗначенияНастроекПрограммы.Настройка.ЭтоГруппа = ЛОЖЬ";
	Запрос.УстановитьПараметр("Настройка", Настройка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НастройкиПрограммы.Вставить(Выборка.Настройка, Выборка.Значение);
	КонецЦикла;
	
	Возврат НастройкиПрограммы;
	
КонецФункции

// Функция - Получить значения настройки для множественной настройки
//
// Параметры:
//  Настройка			 - ПланВидовХарактеристикСсылка.НастройкиПрограммы	 - Ссылка на настройку
// 
// Возвращаемое значение:
//   - Массив
//
Функция ПолучитьЗначенияНастройки(Настройка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗначенияНастроекПрограммы.Значение КАК Значение
		|ИЗ
		|	РегистрСведений.ЗначенияНастроекПрограммы КАК ЗначенияНастроекПрограммы
		|ГДЕ
		|	ЗначенияНастроекПрограммы.Настройка = &Настройка";
	
	Запрос.УстановитьПараметр("Настройка", Настройка);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Значение");

	Возврат РезультатЗапроса;
	
КонецФункции		

#КонецОбласти

#КонецЕсли
