#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура Отразить(Текст) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	мз = РегистрыСведений.ОтправленныеУведомления.СоздатьМенеджерЗаписи();
	мз.ХэшТекста = ХэшТекста(Текст);
	мз.Дата = ТекущаяДатаСеанса();
	мз.Текст = Текст;
	мз.Записать();
	
КонецПроцедуры

Функция ПоследняяДатаОтправки(Текст) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтправленныеУведомления.Дата КАК Дата
	|ИЗ
	|	РегистрСведений.ОтправленныеУведомления КАК ОтправленныеУведомления
	|ГДЕ
	|	ОтправленныеУведомления.ХэшТекста = &ХэшТекста
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ";
	Запрос.УстановитьПараметр("ХэшТекста", ХэшТекста(Текст));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПоследняяДатаОтправки = Выборка.Дата;
	Иначе
		ПоследняяДатаОтправки = '00010101';
	КонецЕсли;
	Возврат ПоследняяДатаОтправки;
	
КонецФункции

#КонецОбласти

Функция ХэшТекста(Текст) Экспорт
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.CRC32);
	ХешированиеДанных.Добавить(Текст);
	Возврат ХешированиеДанных.ХешСумма;
	
КонецФункции


#КонецЕсли
