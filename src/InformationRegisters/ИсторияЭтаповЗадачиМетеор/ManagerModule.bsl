#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура Отразить(ЗадачаМетеор, Этап, Исполнитель, стрИсполнитель, Тип, Комментарий="", Знач ДатаСобытия = '00010101') Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	мз = РегистрыСведений.ИсторияЭтаповЗадачиМетеор.СоздатьМенеджерЗаписи();
	мз.ЗадачаМетеор = ЗадачаМетеор;
	мз.Дата = ?(ЗначениеЗаполнено(ДатаСобытия), ДатаСобытия, ТекущаяДатаСеанса());
	мз.Этап = Этап;
	мз.Исполнитель = Исполнитель;
	мз.стрИсполнитель = стрИсполнитель;
	мз.Тип = Тип;
	МЗ.Комментарий = Комментарий;
	мз.Записать();
	
	РегистрыСведений.ТекущийЭтапЗадачиМетеор.Отразить(ЗадачаМетеор, Этап, Исполнитель, Тип);
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

Функция Этап(ЗадачаМетеор, Дата) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИсторияЭтаповЗадачиМетеор.Этап КАК Этап
	|ИЗ
	|	РегистрСведений.ИсторияЭтаповЗадачиМетеор КАК ИсторияЭтаповЗадачиМетеор
	|ГДЕ
	|	ИсторияЭтаповЗадачиМетеор.ЗадачаМетеор = &ЗадачаМетеор
	|	И ИсторияЭтаповЗадачиМетеор.Дата <= &Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИсторияЭтаповЗадачиМетеор.Дата УБЫВ";
	Запрос.УстановитьПараметр("ЗадачаМетеор", ЗадачаМетеор);
	Запрос.УстановитьПараметр("Дата", Дата);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Этап = Выборка.Этап;
	Иначе
		Этап = Справочники.ЭтапыЗадачиМетеор.ПустаяСсылка();
	КонецЕсли;
	Возврат Этап;
	
КонецФункции

Функция ИсполнительЭтапа(ЗадачаМетеор, Этап) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИсторияЭтаповЗадачиМетеор.Исполнитель КАК Исполнитель
	|ИЗ
	|	РегистрСведений.ИсторияЭтаповЗадачиМетеор КАК ИсторияЭтаповЗадачиМетеор
	|ГДЕ
	|	ИсторияЭтаповЗадачиМетеор.ЗадачаМетеор = &ЗадачаМетеор
	|	И ИсторияЭтаповЗадачиМетеор.Этап = &Этап
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИсторияЭтаповЗадачиМетеор.Дата УБЫВ";
	Запрос.УстановитьПараметр("ЗадачаМетеор", ЗадачаМетеор);
	Запрос.УстановитьПараметр("Этап", Этап);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Исполнитель = Выборка.Исполнитель;
	Иначе
		Исполнитель = Справочники.Пользователи.ПустаяСсылка();
	КонецЕсли;
	Возврат Исполнитель;
	
КонецФункции

#КонецОбласти

#КонецЕсли
