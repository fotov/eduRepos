Процедура ОбновитьОтвественных(Источник) Экспорт
	
	Если ТипЗнч(Источник) = Тип("БизнесПроцессСсылка.ПредварительнаяПроверка") 
		ИЛИ ТипЗнч(Источник) = Тип("БизнесПроцессСсылка.СогласованиеПереноса") Тогда
	
		Набор = РегистрыСведений.ОтветственныеПоИсточникам.СоздатьНаборЗаписей();
		Набор.Отбор.Источник.Установить(Источник);
		
		ДобавитьЗапись(Набор, Источник.Аналитик);
		ДобавитьЗапись(Набор, Источник.Разработчик);
		ДобавитьЗапись(Набор, Источник.Исполнитель);
		ДобавитьЗапись(Набор, Источник.Проверил_ПроверкаКода);
		
		ТЗ = Набор.Выгрузить();
		ТЗ.Свернуть("Источник, Ответственный");
		Набор.Загрузить(ТЗ);
		
		Набор.Записать();
		
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникСсылка.ИсторияХранилища") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ИсторияХранилища.Ссылка КАК Источник,
			|	Пользователи.Ссылка КАК Ответственный
			|ИЗ
			|	Справочник.ИсторияХранилища КАК ИсторияХранилища
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
			|		ПО ИсторияХранилища.Автор = Пользователи.Код
			|ГДЕ
			|	ИсторияХранилища.Ссылка = &Источник
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЗадачиМетеорВерсийХранилища.ВерсияХранилища,
			|	ТекущийЭтапЗадачиМетеор.Исполнитель
			|ИЗ
			|	РегистрСведений.ЗадачиМетеорВерсийХранилища КАК ЗадачиМетеорВерсийХранилища
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТекущийЭтапЗадачиМетеор КАК ТекущийЭтапЗадачиМетеор
			|		ПО ЗадачиМетеорВерсийХранилища.ЗадачаМетеор = ТекущийЭтапЗадачиМетеор.ЗадачаМетеор
			|ГДЕ
			|	ЗадачиМетеорВерсийХранилища.ВерсияХранилища = &Источник";
		Запрос.УстановитьПараметр("Источник", Источник);
		
		Набор = РегистрыСведений.ОтветственныеПоИсточникам.СоздатьНаборЗаписей();
		Набор.Отбор.Источник.Установить(Источник);
		ТЗ = Запрос.Выполнить().Выгрузить();
		ТЗ.Свернуть("Источник, Ответственный");
		Набор.Загрузить(ТЗ);
		Набор.Записать();
		
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникСсылка.ЗадачиМетеор") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ЗадачиМетеорВерсийХранилища.ВерсияХранилища КАК Источник,
			|	ТекущийЭтапЗадачиМетеор.Исполнитель КАК Ответственный
			|ИЗ
			|	РегистрСведений.ЗадачиМетеорВерсийХранилища КАК ЗадачиМетеорВерсийХранилища
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТекущийЭтапЗадачиМетеор КАК ТекущийЭтапЗадачиМетеор
			|		ПО ЗадачиМетеорВерсийХранилища.ЗадачаМетеор = ТекущийЭтапЗадачиМетеор.ЗадачаМетеор
			|ГДЕ
			|	ЗадачиМетеорВерсийХранилища.ЗадачаМетеор = &Источник";
		Запрос.УстановитьПараметр("Источник", Источник);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Не ЗначениеЗаполнено(Выборка.Ответственный) Тогда
				Продолжить;
			КонецЕсли;
			Запись = РегистрыСведений.ОтветственныеПоИсточникам.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			Запись.Записать();
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьЗапись(Набор, Ответственный)
	Если ЗначениеЗаполнено(Ответственный) Тогда
		Запись = Набор.Добавить();
		Запись.Источник = Набор.Отбор.Источник.Значение;
		Запись.Ответственный = Ответственный;
	КонецЕсли;
КонецПроцедуры

Процедура Перезаполнить() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	БизнесПроцесс.Ссылка КАК Источник,
		|	БизнесПроцесс.Аналитик КАК Ответственный
		|ПОМЕСТИТЬ Ответственные
		|ИЗ
		|	БизнесПроцесс.ПредварительнаяПроверка КАК БизнесПроцесс
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	БизнесПроцесс.Ссылка,
		|	БизнесПроцесс.Разработчик
		|ИЗ
		|	БизнесПроцесс.ПредварительнаяПроверка КАК БизнесПроцесс
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	БизнесПроцесс.Ссылка,
		|	БизнесПроцесс.Исполнитель
		|ИЗ
		|	БизнесПроцесс.ПредварительнаяПроверка КАК БизнесПроцесс
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	БизнесПроцесс.Ссылка,
		|	БизнесПроцесс.Проверил_ПроверкаКода
		|ИЗ
		|	БизнесПроцесс.ПредварительнаяПроверка КАК БизнесПроцесс
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	БизнесПроцесс.Ссылка,
		|	БизнесПроцесс.Аналитик
		|ИЗ
		|	БизнесПроцесс.СогласованиеПереноса КАК БизнесПроцесс
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	БизнесПроцесс.Ссылка,
		|	БизнесПроцесс.Разработчик
		|ИЗ
		|	БизнесПроцесс.СогласованиеПереноса КАК БизнесПроцесс
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	БизнесПроцесс.Ссылка,
		|	БизнесПроцесс.Исполнитель
		|ИЗ
		|	БизнесПроцесс.СогласованиеПереноса КАК БизнесПроцесс
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	БизнесПроцесс.Ссылка,
		|	БизнесПроцесс.Проверил_ПроверкаКода
		|ИЗ
		|	БизнесПроцесс.СогласованиеПереноса КАК БизнесПроцесс
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИсторияХранилища.Ссылка,
		|	Пользователи.Ссылка
		|ИЗ
		|	Справочник.ИсторияХранилища КАК ИсторияХранилища
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО ИсторияХранилища.Автор = Пользователи.Код
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗадачиМетеорВерсийХранилища.ВерсияХранилища,
		|	ТекущийЭтапЗадачиМетеор.Исполнитель
		|ИЗ
		|	РегистрСведений.ЗадачиМетеорВерсийХранилища КАК ЗадачиМетеорВерсийХранилища
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТекущийЭтапЗадачиМетеор КАК ТекущийЭтапЗадачиМетеор
		|		ПО ЗадачиМетеорВерсийХранилища.ЗадачаМетеор = ТекущийЭтапЗадачиМетеор.ЗадачаМетеор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Ответственные.Источник КАК Источник,
		|	Ответственные.Ответственный КАК Ответственный
		|ИЗ
		|	Ответственные КАК Ответственные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО Ответственные.Ответственный = Пользователи.Ссылка";
	Набор = РегистрыСведений.ОтветственныеПоИсточникам.СоздатьНаборЗаписей();
	Набор.Загрузить(Запрос.Выполнить().Выгрузить());
	Набор.Записать();
КонецПроцедуры