#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область СведенияОВнешнейОбработке
// Возвращает сведения о внешней обработке.
//
// Возвращаемое значение:
//   Структура - Подробнее см. ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке().
//
Функция СведенияОВнешнейОбработке() Экспорт
		
	Мета = ЭтотОбъект.Метаданные();
	Версия = Мета.Комментарий;
	ВерсияБСП = "3.1.7.82";
	ТекущаяВерсияБСП = СтандартныеПодсистемыСервер.ВерсияБиблиотеки();
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке(ВерсияБСП);
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительныйОтчет();
	ПараметрыРегистрации.Версия = Версия;
	ПараметрыРегистрации.Наименование = Мета.Представление();
	ПараметрыРегистрации.Информация = Мета.Представление();
	ПараметрыРегистрации.Назначение.Добавить("Подсистема.ПереносИзменений");
	ПараметрыРегистрации.БезопасныйРежим = Ложь;
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Представление = "_" + Мета.Представление();
	НоваяКоманда.Идентификатор = Мета.ПолноеИмя();
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	НоваяКоманда.ПоказыватьОповещение = Ложь;
	
	Возврат ПараметрыРегистрации;
	
КонецФункции
#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СхемаКомпоновки = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	НастройкиКомпоновки = КомпоновщикНастроек.ПолучитьНастройки();
	
	Конфигурация = Справочники.Конфигурации.ПустаяСсылка();
	ЭлементыОтбора = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(НастройкиКомпоновки.Отбор, "Конфигурация");
	Если ЭлементыОтбора.Количество() 
			И ЭлементыОтбора[0].Использование 
			И ЭлементыОтбора[0].ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
		Конфигурация = ЭлементыОтбора[0].ПравоеЗначение;
	КонецЕсли;
	
	ПараметрПериод = ОтчетыКлиентСервер.НайтиПараметр(НастройкиКомпоновки, Неопределено, "Период");
	НачалоПериода = ПараметрПериод.Значение.ДатаНачала;
	КонецПериода = ПараметрПериод.Значение.ДатаОкончания;
	
	
	ИзмененияОбъетов = ИзмененияОбъетов(НачалоПериода, КонецПериода, Конфигурация);
	
	ВнешниеНаборыДанных = Новый Структура;
    ВнешниеНаборыДанных.Вставить("ИзмененияОбъетов", ИзмененияОбъетов);
	
	КомпановщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;    
	МакетКомпановки = КомпановщикМакета.Выполнить(СхемаКомпоновки, НастройкиКомпоновки, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпановки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИзмененияОбъетов(НачалоПериода, КонецПериода, Конфигурация)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсторияХранилища.Ссылка КАК ВерсияХО,
		|	ИсторияХранилища.Владелец.Владелец КАК Конфигурация,
		|	ИсторияХранилища.Комментарий КАК Комментарий
		|ПОМЕСТИТЬ ВерсииХО
		|ИЗ
		|	Справочник.ИсторияХранилища КАК ИсторияХранилища
		|ГДЕ
		|	ИсторияХранилища.Владелец В
		|			(ВЫБРАТЬ
		|				Конфигурации.ХранилищеОбновления КАК ХранилищеОбновления
		|			ИЗ
		|				Справочник.Конфигурации КАК Конфигурации)
		|	И ИсторияХранилища.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И (&Конфигурация = ЗНАЧЕНИЕ(Справочник.Конфигурации.ПустаяСсылка)
		|			ИЛИ ИсторияХранилища.Владелец.Владелец = &Конфигурация)
		|{ГДЕ
		|	ИсторияХранилища.Владелец.Владелец.* КАК Конфигурация}
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВерсииХО.ВерсияХО КАК ВерсияХО,
		|	ВерсииХО.Конфигурация КАК Конфигурация,
		|	ВерсииХО.Комментарий КАК Комментарий,
		|	СогласованиеПереносаКоммиты.Коммит КАК ВерсияХР,
		|	СогласованиеПереносаКоммиты.Коммит.Автор КАК АвторХР
		|ПОМЕСТИТЬ ВерсииХРПоПереносу
		|ИЗ
		|	ВерсииХО КАК ВерсииХО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.СогласованиеПереноса.Коммиты КАК СогласованиеПереносаКоммиты
		|		ПО ВерсииХО.ВерсияХО = СогласованиеПереносаКоммиты.КоммитХО
		|			И (НЕ СогласованиеПереносаКоммиты.Коммит = ЗНАЧЕНИЕ(Справочник.ИсторияХранилища.ПустаяСсылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВерсииХРПоПереносу.ВерсияХО КАК ВерсияХО,
		|	ВерсииХРПоПереносу.Конфигурация КАК Конфигурация,
		|	ВерсииХРПоПереносу.Комментарий КАК Комментарий,
		|	ВерсииХРПоПереносу.ВерсияХР КАК ВерсияХР,
		|	ЕСТЬNULL(Пользователи.Ссылка, ""Не найден"") КАК АвторВерсииХР,
		|	ЗадачиМетеорВерсийХранилища.ЗадачаМетеор КАК ЗадачаМетеор,
		|	ВЫРАЗИТЬ(ЗадачиМетеорВерсийХранилища.ЗадачаМетеор.НаименованиеЗадачи КАК СТРОКА(1024)) КАК ЗадачаМетеорНаименование
		|ПОМЕСТИТЬ ВерсииХОиХР
		|ИЗ
		|	ВерсииХРПоПереносу КАК ВерсииХРПоПереносу
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО ВерсииХРПоПереносу.АвторХР = Пользователи.Код
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиМетеорВерсийХранилища КАК ЗадачиМетеорВерсийХранилища
		|		ПО ВерсииХРПоПереносу.ВерсияХР = ЗадачиМетеорВерсийХранилища.ВерсияХранилища
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВерсииХО.ВерсияХО,
		|	ВерсииХО.Конфигурация,
		|	ВерсииХО.Комментарий,
		|	ВерсииХО.ВерсияХО,
		|	""Не найден"",
		|	ЗНАЧЕНИЕ(Справочник.ЗадачиМетеор.ПустаяСсылка),
		|	""""
		|ИЗ
		|	ВерсииХО КАК ВерсииХО
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВерсииХРПоПереносу КАК ВерсииХРПоПереносу
		|		ПО ВерсииХО.ВерсияХО = ВерсииХРПоПереносу.ВерсияХО
		|ГДЕ
		|	ВерсииХРПоПереносу.ВерсияХО ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВерсииХОиХР.ВерсияХО КАК ВерсияХО,
		|	ВерсииХОиХР.Конфигурация КАК Конфигурация,
		|	ВерсииХОиХР.Комментарий КАК Комментарий,
		|	ВерсииХОиХР.ВерсияХР КАК ВерсияХР,
		|	ВерсииХОиХР.АвторВерсииХР КАК АвторВерсииХР,
		|	ВерсииХОиХР.ЗадачаМетеор КАК ЗадачаМетеор,
		|	ВерсииХОиХР.ЗадачаМетеорНаименование КАК ЗадачаМетеорНаименование,
		|	ИсторияХранилищаИзменены.ИмяОбъекта КАК ИмяОбъекта,
		|	""Изменены"" КАК ВидИзменения
		|ПОМЕСТИТЬ ВсеИзменения
		|ИЗ
		|	ВерсииХОиХР КАК ВерсииХОиХР
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИсторияХранилища.Изменены КАК ИсторияХранилищаИзменены
		|		ПО ВерсииХОиХР.ВерсияХР = ИсторияХранилищаИзменены.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВерсииХОиХР.ВерсияХО,
		|	ВерсииХОиХР.Конфигурация,
		|	ВерсииХОиХР.Комментарий,
		|	ВерсииХОиХР.ВерсияХР,
		|	ВерсииХОиХР.АвторВерсииХР,
		|	ВерсииХОиХР.ЗадачаМетеор,
		|	ВерсииХОиХР.ЗадачаМетеорНаименование,
		|	ИсторияХранилищаДобавлены.ИмяОбъекта,
		|	""Добавлены""
		|ИЗ
		|	ВерсииХОиХР КАК ВерсииХОиХР
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИсторияХранилища.Добавлены КАК ИсторияХранилищаДобавлены
		|		ПО ВерсииХОиХР.ВерсияХР = ИсторияХранилищаДобавлены.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВерсииХОиХР.ВерсияХО,
		|	ВерсииХОиХР.Конфигурация,
		|	ВерсииХОиХР.Комментарий,
		|	ВерсииХОиХР.ВерсияХР,
		|	ВерсииХОиХР.АвторВерсииХР,
		|	ВерсииХОиХР.ЗадачаМетеор,
		|	ВерсииХОиХР.ЗадачаМетеорНаименование,
		|	ИсторияХранилищаУдалены.ИмяОбъекта,
		|	""Удалены""
		|ИЗ
		|	ВерсииХОиХР КАК ВерсииХОиХР
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИсторияХранилища.Удалены КАК ИсторияХранилищаУдалены
		|		ПО ВерсииХОиХР.ВерсияХР = ИсторияХранилищаУдалены.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВсеИзменения.ВерсияХО КАК ВерсияХО,
		|	ВсеИзменения.Конфигурация КАК Конфигурация,
		|	ВсеИзменения.Комментарий КАК Комментарий,
		|	ВсеИзменения.АвторВерсииХР КАК АвторВерсииХР,
		|	ВсеИзменения.ЗадачаМетеор КАК ЗадачаМетеор,
		|	ВсеИзменения.ЗадачаМетеорНаименование КАК ЗадачаМетеорНаименование,
		|	ВсеИзменения.ИмяОбъекта КАК ИмяОбъекта,
		|	ВсеИзменения.ВидИзменения КАК ВидИзменения,
		|	ИсторияЭтаповЗадачиМетеор.стрИсполнитель КАК Аналитик
		|ИЗ
		|	ВсеИзменения КАК ВсеИзменения
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияЭтаповЗадачиМетеор КАК ИсторияЭтаповЗадачиМетеор
		|		ПО ВсеИзменения.ЗадачаМетеор = ИсторияЭтаповЗадачиМетеор.ЗадачаМетеор
		|			И (ИсторияЭтаповЗадачиМетеор.Этап = &ЭтапАналитическаяПроработка)";
	
	Запрос.УстановитьПараметр("Конфигурация", Конфигурация);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ЭтапАналитическаяПроработка", Справочники.ЭтапыЗадачиМетеор.АналитическаяПроработка);
	
	Результат = Запрос.Выполнить().Выгрузить();
	ДанныеЗадач = Неопределено;
	Попытка
		ДанныеЗадач = ИнтеграцияСМетеор.ПолучитьДанныеЗадачМетеор(Результат.ВыгрузитьКолонку("ЗадачаМетеор"));
	Исключение
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки;
	Если ДанныеЗадач = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	Для Каждого СтрокаРезультата Из Результат Цикл
		АдминистраторЗадачи = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ДанныеЗадач.Получить(СтрокаРезультата.ЗадачаМетеор), "admin");
		СтрокаРезультата.Аналитик = ?(АдминистраторЗадачи = Неопределено, СтрокаРезультата.Аналитик, АдминистраторЗадачи);
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Иначе
  ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
