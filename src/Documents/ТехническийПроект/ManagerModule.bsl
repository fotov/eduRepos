
Функция СформироватьHtmlОписание(ДокументСсылкаТехПроект) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТехническийПроект.Номер КАК Номер,
	               |	ТехническийПроект.Дата КАК Дата,
	               |	ТехническийПроект.Описание КАК Описание,
	               |	ТехническийПроект.Ответственный КАК Ответственный
	               |ИЗ
	               |	Документ.ТехническийПроект КАК ТехническийПроект
	               |ГДЕ
	               |	ТехническийПроект.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТехническийПроектКонфигурации.Конфигурация КАК Конфигурация,
	               |	ТехническийПроектКонфигурации.Конфигурация.Наименование КАК КонфигурацияНаименование
	               |ИЗ
	               |	Документ.ТехническийПроект.Конфигурации КАК ТехническийПроектКонфигурации
	               |ГДЕ
	               |	ТехническийПроектКонфигурации.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТехническийПроектИзменения.Действие.Наименование КАК Действие,
	               |	ТехническийПроектИзменения.Конфигурация.Наименование КАК Конфигурация,
	               |	ЕСТЬNULL(ТехническийПроектИзменения.ТипОбъекта.Наименование, """") КАК ТипОбъекта,
	               |	ВЫБОР
	               |		КОГДА ТехническийПроектИзменения.Объект ССЫЛКА Справочник.ОбъектыМетаданных
	               |			ТОГДА ЕСТЬNULL(ТехническийПроектИзменения.Объект.Наименование, """")
	               |		ИНАЧЕ ТехническийПроектИзменения.Объект
	               |	КОНЕЦ КАК Объект,
	               |	ТехническийПроектИзменения.ОбъектВладелец КАК ОбъектВладелец,
	               |	ТехническийПроектИзменения.ОписаниеИзменений КАК ОписаниеИзменений,
	               |	ТехническийПроектИзменения.НетИзмененийМетаданных КАК НетИзмененийМетаданных,
	               |	ТехническийПроектИзменения.Ключ КАК Ключ
	               |ИЗ
	               |	Документ.ТехническийПроект.Изменения КАК ТехническийПроектИзменения
	               |ГДЕ
	               |	ТехническийПроектИзменения.Ссылка = &Ссылка
	               |ИТОГИ ПО
	               |	Конфигурация.Наименование
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТехническийПроектСвойстваОбъекта.Ключ КАК Ключ,
	               |	ТехническийПроектСвойстваОбъекта.Свойство.Наименование КАК Свойство,
	               |	ТехническийПроектСвойстваОбъекта.Значение КАК Значение
	               |ИЗ
	               |	Документ.ТехническийПроект.СвойстваОбъекта КАК ТехническийПроектСвойстваОбъекта
	               |ГДЕ
	               |	ТехническийПроектСвойстваОбъекта.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаТехПроект);
	
	РезультатЗапроса =  Запрос.ВыполнитьПакет();
	
	ШаблонТекстТехПроекта = "<html><head> <style type=""text/css"">
   |TABLE {
   | border-collapse: collapse; /* Убираем двойные линии между ячейками */
   |}
   |TD {
   | border: 0px groove #ccc; /* Рамка вокруг таблицы */
   | padding: 3px; /* Поля вокруг содержимого ячеек */
   |}
   |</style></head><body><P><center><B>%1</B></center></P><P>%2</P><P>%3</P></body></html>";
	
	ВыборкаШапка = РезультатЗапроса[0].Выбрать();
	ВыборкаШапка.Следующий();
	ТекстОписание = "<h3>Общее описание</h3>"+СтрЗаменить(ВыборкаШапка.Описание, Символы.ПС, "</BR>");	
	
	ТекстБазы = "";
	//ВыборкаБазы = РезультатЗапроса[1].Выбрать();
	//Пока ВыборкаБазы.Следующий() Цикл
	//	ТекстБазы = ТекстБазы + ?(ТекстБазы= "","","; ") + ВыборкаБазы.КонфигурацияНаименование;
	//КонецЦикла;
	//ТекстБазы = "Базы: "+ТекстБазы;
	
	
	ВыборкаИзмененияК =  РезультатЗапроса[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ТаблицаСвойства =  РезультатЗапроса[3].Выгрузить();
	//ШаблонДействиеСОбъектом = "%1.%2";
	ШаблонТаблицы = "<TABLE>%1</TABLE>";
	ШаблонСтроки1 = "<TR><TD colspan = 2>%1</TD></TR>";
	ШаблонСтроки2 = "<TR><TD>%1</TD><TD>%2</TD></TR>";
	ШаблонСтроки3 = "<TR><TD>%1</TD><TD>%2</TD></TR>";
	ШаблонСтроки1и2 = "<TR><TD colspan = 2; style='padding-left:30px'>%2</TD></TR>";
	
	ТекстИзменения = "";
	Пока ВыборкаИзмененияК.Следующий() Цикл
		ВыборкаИзменения = ВыборкаИзмененияК.Выбрать();		
		ТекстИзменения = ТекстИзменения + Символы.ПС + СтрШаблон(ШаблонСтроки1, "<CENTER><B>"+ВыборкаИзмененияК.Конфигурация+"</B></CENTER>");
		Пока ВыборкаИзменения.Следующий() Цикл
			ТекстИзменения = ТекстИзменения + Символы.ПС + СтрШаблон(ШаблонСтроки3, "<B>"+ВыборкаИзменения.Действие+"</B> "+ ВыборкаИзменения.ТипОбъекта+" "+ВыборкаИзменения.Объект, "");
			
			СтрокиСвойства = ТаблицаСвойства.НайтиСтроки(Новый Структура("Ключ",ВыборкаИзменения.Ключ));
			ТекстСвойств = "";
			Для Каждого СтрокаСвойство Из СтрокиСвойства Цикл
				Если НЕ ПустаяСтрока(СтрокаСвойство.Значение) Тогда
					ТекстСвойств = ТекстСвойств + ?(ПустаяСтрока(ТекстСвойств),"","</BR>") + "<i>"+СтрокаСвойство.Свойство+":</i>&nbsp" + СтрокаСвойство.Значение+" ;";	
				КонецЕсли;
			КонецЦикла;
			Если НЕ ПустаяСтрока(ВыборкаИзменения.ОписаниеИзменений) Тогда
				ТекстСвойств = ВыборкаИзменения.ОписаниеИзменений + ?(ПустаяСтрока(ТекстСвойств),"","</BR></BR>"+ ТекстСвойств);
			КонецЕсли;
			Если НЕ ПустаяСтрока(ТекстСвойств) Тогда
				ТекстИзменения = ТекстИзменения + Символы.ПС + СтрШаблон(ШаблонСтроки1и2, "", ТекстСвойств );	
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	ТекстИзменения = СтрШаблон(ШаблонТаблицы, ТекстИзменения);
		
	ТекстТехПроекта = СтрШаблон(ШаблонТекстТехПроекта, ТекстБазы, ТекстИзменения, ТекстОписание);
	Возврат ТекстТехПроекта;
	
КонецФункции