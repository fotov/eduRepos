#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область ОбработчикиПроведения

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение и Не ЗначениеЗаполнено(ДатаОтправки) Тогда 
		Отказ = Не ОтправитьПисьмо();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОтправитьПисьмо() Экспорт
	
	Если БлокировкаРаботыСВнешнимиРесурсамиУКПовтИсп.ЭтоКопияБазы() Тогда
		Сообщить("В копии отправка писем заблокированна");
		Возврат Ложь;
	КонецЕсли;
	
	ДатаОтправки = ТекущаяДатаСеанса();
	
	ПараметрыПисьма = РаботаСПочтовымиСообщениями.ПолучитьПараметрыПисьма();
	ПараметрыПисьма.Тело 			= Тело;
	ПараметрыПисьма.Тема 			= Тема;
	ПараметрыПисьма.АдресПолучателя = Получатели;	
	ПараметрыПисьма = РаботаСПочтовымиСообщениями.ПодготовитьПараметрыПисьма(ПараметрыПисьма);
	ОписаниеОшибки = "";
	ТекущийТекстHTML = ПараметрыПисьма.Тело;
	
	ПочтовыеВложения = Новый ТаблицаЗначений;
	ПочтовыеВложения.Колонки.Добавить("Ключ");
	ПочтовыеВложения.Колонки.Добавить("Значение");
	ПочтовыеВложения.Колонки.Добавить("Расширение");
	
	Для Каждого Вложение Из Вложения Цикл
		
		НоваяСтрока = ПочтовыеВложения.Добавить();
		НоваяСтрока.Ключ = Вложение.Ключ;
		НоваяСтрока.Значение = Вложение.Значение.Получить();
		НоваяСтрока.Расширение = Вложение.Расширение;
		
	КонецЦикла;
	
	Результат = РаботаСПочтовымиСообщениями.ОтправитьСообщение(ПараметрыПисьма, ОписаниеОшибки, ТекущийТекстHTML,, Истина, ПочтовыеВложения);
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		ЗаписьЖурналаРегистрации(
			"РаботаСПочтовымиСообщениями",
			,
			,
			Ссылка,
			СтрШаблон("При отправке электронного письма возникли ошибки.
				|Письмо: %1
				|Ошибки:
				|%2",
				Ссылка,
				ОписаниеОшибки));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Иначе
  ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
