
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("База", База);
	Параметры.Свойство("Дополнение", Дополнение);
	Параметры.Свойство("Режим", Режим);
	Параметры.Свойство("ТипДополнения", ТипДополнения);

	НастроитьФорму(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Режим = "ЗагрузитьИУстановить" Тогда
		ОбработчикЗавершения = Новый ОписаниеОповещения("ПослеСозданияВерсииДополнения", ЭтотОбъект);
		ПараметрыДобавленияВерсии = Новый Структура;
		ПараметрыДобавленияВерсии.Вставить("РежимВыбора", Истина);
		ПараметрыДобавленияВерсии.Вставить("ПоказатьДиалогЗагрузкиИзФайлаПриОткрытии", Истина);
		Если ЗначениеЗаполнено(ТипДополнения) Тогда
			ПараметрыДобавленияВерсии.Вставить("ТипДополнения", ТипДополнения);
		КонецЕсли;
		
		ФормаНовойВерсии = ОткрытьФорму(
			"Справочник.ВерсииДополнений.ФормаОбъекта",
			ПараметрыДобавленияВерсии,
			Элементы.Версия,
			УникальныйИдентификатор,
			,
			,
			ОбработчикЗавершения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура БазаПриИзменении(Элемент)
	НастроитьФорму(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ВерсияПриИзменении(Элемент)
	НастроитьФорму(ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура Установить(Команда)
	Если Не ДополнениеСогласовано(ВерсияДополнения) Тогда
		Ответ = Ждать ВопросАсинх("Дополнение не согласовано. Продолжить?", РежимДиалогаВопрос.ОКОтмена,, КодВозвратаДиалога.Отмена, "Внимание!");
		Если Ответ = КодВозвратаДиалога.Отмена Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Результат = Новый Структура("База, Дополнение, ВерсияДополнения", База, Дополнение, ВерсияДополнения);
	Закрыть(Результат);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьФорму(Форма)
	ДоступностьУстановки = НЕ (Форма.База.Пустая() Или Форма.ВерсияДополнения.Пустая());
	Форма.Элементы.Установить.Доступность = ДоступностьУстановки;
КонецПроцедуры

&НаКлиенте
Процедура ПослеСозданияВерсииДополнения(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ВерсияДополнения) Тогда
		Закрыть(Неопределено);
		Возврат;
	КонецЕсли;
	
	ПослеСозданияВерсииДополненияНаСервере();
	НастроитьФорму(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПослеСозданияВерсииДополненияНаСервере()
	
	РеквизитыДополнения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВерсияДополнения, "Владелец, Владелец.Конфигурация");
	Дополнение = РеквизитыДополнения.Владелец;
	
	ПараметрВыбораПоКонфигурации = Новый ПараметрВыбора("Отбор.Конфигурация", РеквизитыДополнения.ВладелецКонфигурация);
	Элементы.База.ПараметрыВыбора = Новый ФиксированныйМассив(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрВыбораПоКонфигурации));
	
	Элементы.Дополнение.ТолькоПросмотр = Истина;
	Элементы.Версия.ТолькоПросмотр = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДополнениеСогласовано(ВерсияДополнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтатусыДополненийСрезПоследних.ВерсияДополнения КАК ВерсияДополнения
		|ИЗ
		|	РегистрСведений.СтатусыДополнений.СрезПоследних(, ВерсияДополнения = &ВерсияДополнения) КАК СтатусыДополненийСрезПоследних
		|ГДЕ
		|	СтатусыДополненийСрезПоследних.Статус = &СтатусСогласовано";
	
	Запрос.УстановитьПараметр("ВерсияДополнения", ВерсияДополнения);
	Запрос.УстановитьПараметр("СтатусСогласовано", Перечисления.СтатусыИсторииХранилищ.Перенос);
	
	РезультатЗапроса = Запрос.Выполнить();

	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти