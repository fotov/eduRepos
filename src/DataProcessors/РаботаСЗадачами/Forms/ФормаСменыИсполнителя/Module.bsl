
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ЗадачаИсполнителя", ЗадачаИсполнителя);
	Параметры.Свойство("ТочкаМаршрута", ТочкаМаршрута);
	
	Если ТочкаМаршрута = ПредопределенноеЗначение("БизнесПроцесс.ПредварительнаяПроверка.ТочкаМаршрута.ПроверкаКода") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	РегистрАдресации.Исполнитель КАК Исполнитель,
			|	РегистрАдресации.Исполнитель.Наименование КАК ИсполнительНаименование
			|ИЗ
			|	РегистрСведений.РегистрАдресации КАК РегистрАдресации
			|ГДЕ
			|	РегистрАдресации.РольИсполнителя = ЗНАЧЕНИЕ(Справочник.РолиИсполнителей.ВедущийПрограммист)
			|	И РегистрАдресации.Исполнитель.Недействителен = ЛОЖЬ
			|	И РегистрАдресации.Исполнитель.ПометкаУдаления = ЛОЖЬ
			|
			|УПОРЯДОЧИТЬ ПО
			|	ИсполнительНаименование";
		Элементы.ЖелаемыйИсполнитель.СписокВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0));
		Элементы.ЖелаемыйИсполнитель.РежимВыбораИзСписка = Истина;
	КонецЕсли;
	
	ЖелаемыйИсполнитель = ЗадачаИсполнителя.ЖелаемыйИсполнитель;
	ИзбранныйИсполнитель = ХранилищеОбщихНастроек.Загрузить(ИмяФормы, "ИзбранныйИсполнитель");
	Элементы.ДекорацияЗвезда.Видимость = ЗначениеЗаполнено(ИзбранныйИсполнитель);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИзбранныйИсполнительНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЖелаемыйИсполнитель = ИзбранныйИсполнитель;
	ПередатьЗадачу(Элемент);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПередатьЗадачу(Команда)
	
	Если Не ЗначениеЗаполнено(ЖелаемыйИсполнитель) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указан исполнитель");
		Возврат;
	КонецЕсли;
	
	НеобходимоОбновитьИнтерфейс = ПередатьЗадачуНаСервере();
	Закрыть(НеобходимоОбновитьИнтерфейс);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПередатьЗадачуНаСервере()
	
	ХранилищеОбщихНастроек.Сохранить(ИмяФормы, "ИзбранныйИсполнитель", ЖелаемыйИсполнитель);
	ЗадачаОбъект = ЗадачаИсполнителя.ПолучитьОбъект();
	Если ЗадачаОбъект.ЖелаемыйИсполнитель = ЖелаемыйИсполнитель Тогда
		Возврат Ложь;
	КонецЕсли;
	ЗадачаОбъект.ЖелаемыйИсполнитель = ЖелаемыйИсполнитель;
	ЗадачаОбъект.Записать();
	Возврат Истина;
	
КонецФункции

#КонецОбласти
