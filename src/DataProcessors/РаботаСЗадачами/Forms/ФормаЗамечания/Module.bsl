&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КомментарийОбязателен = Истина;
	
	Если Параметры.Свойство("Задача") И Параметры.Свойство("ИсточникЗамечаний") Тогда
		ЗаполнитьКомментарийПоЗадаче(Параметры.Задача, Параметры.ИсточникЗамечаний);
	КонецЕсли;
	
	Элементы.ФормаПропустить.Видимость = Не КомментарийОбязателен;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если КомментарийОбязателен И Не ЗначениеЗаполнено(Комментарий.ПолучитьТекст()) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не заполнено поле комментарий", , , , Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьЗамечание(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть(Новый Структура("ДанныеЗамечания", СохранитьЗамечаниеНаСервере()));
	
КонецПроцедуры

&НаСервере
Функция СохранитьЗамечаниеНаСервере()
	
	ТекстHTML = "";
	Вложения = Новый Структура;
	Комментарий.ПолучитьHTML(ТекстHTML, Вложения);
	
	ДанныеЗамечания = Новый Структура;
	ДанныеЗамечания.Вставить("HTML", ТекстHTML);
	ДанныеЗамечания.Вставить("Текст", Комментарий.ПолучитьТекст());
	ДанныеЗамечания.Вставить("хзВложения", Новый ХранилищеЗначения(Вложения, Новый СжатиеДанных(9)));
	Возврат ДанныеЗамечания;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьКомментарийПоЗадаче(Задача, ИсточникЗамечаний)

	ЗадачаМетеор = Задачи.ЗадачаИсполнителя.ЗадачаМетеор(Задача);
	
	Замечания = РегистрыСведений.ЗамечанияКоммита.ЗамечанияПоЗадаче(ЗадачаМетеор, ИсточникЗамечаний);
	
	Комментарий.Добавить(Замечания);

КонецПроцедуры
