
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ (Параметры.Свойство("ЗадачаМетеор", ЗадачаМетеор) И ЗначениеЗаполнено(ЗадачаМетеор)) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указана задача.",,,,Отказ);
		Возврат;
	КонецЕсли;
	
	Если НЕ (Параметры.Свойство("КодРоли", Роль) И ЗначениеЗаполнено(Роль)) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указана роль.",,,,Отказ);
		Возврат;
	КонецЕсли;
	
	ВремяС  = ТекущаяДатаСеанса();
	ВремяПо = ВремяС;

	Описание = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Описание", "Аудит кода");
	
	user_id = Пользователи.ТекущийПользователь().МетеорИД;
	Если НЕ ЗначениеЗаполнено(user_id) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("У текущего пользователя не заполнен реквизит ""Метеор ИД"". Добавление часов невозможно.",,,,Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВремяСПриИзменении(Элемент)
	ВремяПо = Макс(ВремяС,ВремяПо);
	ПересчитатьОбщееВремя();
КонецПроцедуры

&НаКлиенте
Процедура ВремяПоПриИзменении(Элемент)
	ВремяС = Мин(ВремяС,ВремяПо);
	ПересчитатьОбщееВремя();
КонецПроцедуры

&НаКлиенте
Процедура ЧасыПриИзменении(Элемент)
	Прибавка = ВремяВСекундах(Часы);
	Если ВремяВСекундах(ВремяПо) > Прибавка Тогда
		ВремяС = ВремяПо - Прибавка;
	Иначе
		ВремяС  = НачалоДня(ВремяС);
		ВремяПо = ВремяС + Прибавка;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьВремя(Команда)
	ДобавитьВремяНаСервере();
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПересчитатьОбщееВремя()
	Часы = НачалоДня(Часы) + Макс(0, ВремяПо - ВремяС);
КонецПроцедуры

&НаСервере
Процедура ДобавитьВремяНаСервере()
	
	Если Не ЗначениеЗаполнено(Часы) Тогда
		Возврат;
	КонецЕсли;
	
	Данные = Новый Структура;
	Данные.Вставить("Роль", Роль); 
	Данные.Вставить("Дата", НачалоДня(ТекущаяДатаСеанса()) + ВремяВСекундах(ВремяС));
	Данные.Вставить("Часы", ВремяВСекундах(Часы));
	Данные.Вставить("НомерЗадачи", ЗадачаМетеор.Код); 
	Данные.Вставить("Описание", Описание);
	Данные.Вставить("user_id", user_id); 

	ИнтеграцияСМетеор.ОтразитьВремяПоЗадачеВМетеоре(Данные);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВремяВСекундах(ТекДата)
	Возврат ТекДата - НачалоДня(ТекДата);
КонецФункции

#КонецОбласти
